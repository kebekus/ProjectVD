/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Divisor
import Mathlib.Analysis.Meromorphic.NormalForm


-- -----------------

/--
Any set is codiscrete within itself.
-/
theorem Filter.codiscreteWithin_self {X : Type*} [TopologicalSpace X] (U : Set X) :
    U âˆˆ Filter.codiscreteWithin U := by simp [mem_codiscreteWithin]

-- -----------------

/--
If `X` is a `T1Space`, then functions with locally finite support within `U`
have discrete support within `U`.
-/
theorem Function.locallyFinsuppWithin.supportDiscreteWithinDomain
    {X : Type*} [TopologicalSpace X] [T1Space X] (U : Set X)
    {Y : Type*} [Zero Y]
    (f : Function.locallyFinsuppWithin U Y) :
    f =á¶ [Filter.codiscreteWithin U] 0 := by
  apply codiscreteWithin_iff_locallyFiniteComplementWithin.2
  have : f.support = (U \ {x | f x = (0 : X â†’ Y) x}) := by
    ext x
    simp only [mem_support, ne_eq, Pi.zero_apply, Set.mem_diff, Set.mem_setOf_eq, iff_and_self]
    exact (support_subset_iff.1 f.supportWithinDomain) x
  rw [â† this]
  exact f.supportLocallyFiniteWithinDomain

-- -----------------

/-!
# Laurent polynomials

This file discusses Laurent polynomials as examples of meromorphic functions in
normal form. Laurent polynomials are functions on a non-trivially normed field
`ğ•œ` of the form `(âˆá¶  u, fun z â†¦ (z - u) ^ d u)`, where `d : ğ•œ â†’ â„¤` has finite
support. We show that Laurent polynomials are meromorphic in normal form, with
divisor equal to `d`.
-/

open Classical Real Topology

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]
  {U : Set ğ•œ}
  {z : ğ•œ}

private lemma analyticAt_finLaurentPolynomial_off_support (d : ğ•œ â†’ â„¤) (P : Finset ğ•œ)
    (hz : z âˆ‰ P) :
    AnalyticAt ğ•œ (âˆ u âˆˆ P, fun z â†¦ (z - u) ^ d u) z := by
  rw [Finset.prod_fn]
  apply Finset.analyticAt_fun_prod
  intro u hu
  apply AnalyticAt.zpow
  fun_prop
  rw [sub_ne_zero, ne_comm]
  exact ne_of_mem_of_not_mem hu hz

/-- Laurent polynomials are meromorphic in normal form on `âŠ¤`. -/
theorem meromorphicNF_LaurentPolynomial (d : ğ•œ â†’ â„¤) :
    MeromorphicNFOn (âˆá¶  u, fun z â†¦ (z - u) ^ d u) âŠ¤ := by
  by_cases hd : (Function.mulSupport fun u z => (z - u) ^ d u).Finite
  Â· rw [finprod_eq_prod _ hd]
    intro z hz
    by_cases hâ‚‚z : z âˆˆ hd.toFinset
    Â· rw [â† Finset.mul_prod_erase hd.toFinset _ hâ‚‚z]
      right
      use d z, âˆ x âˆˆ hd.toFinset.erase z, fun z => (z - x) ^ d x,
        analyticAt_finLaurentPolynomial_off_support d (hd.toFinset.erase z)
          (Finset.not_mem_erase z hd.toFinset)
      constructor
      Â· rw [Finset.prod_apply, Finset.prod_ne_zero_iff]
        intro u hu
        apply zpow_ne_zero
        rw [sub_ne_zero]
        by_contra hCon
        rw [hCon] at hu
        have := Finset.not_mem_erase u hd.toFinset
        tauto
      Â· exact Filter.Eventually.of_forall (congrFun rfl)
    Â· exact (analyticAt_finLaurentPolynomial_off_support d hd.toFinset hâ‚‚z).meromorphicNFAt
  Â· rw [finprod_of_infinite_mulSupport hd]
    apply analyticOnNhd_const.meromorphicNFOn

/-- Laurent polynomials are meromorphic in normal form on arbitrary subsets of `ğ•œ`. -/
theorem MeromorphicNFOn_LaurentPolynomial (d : ğ•œ â†’ â„¤) (U : Set ğ•œ) :
    MeromorphicNFOn (âˆá¶  u, fun z â†¦ (z - u) ^ d u) U := by
  intro z hz
  exact meromorphicNF_LaurentPolynomial d (trivial)

/--
Helper Lemma: Identifying the support of `d` as the mulsupport of the product
defining the Laurent polynomial.
-/
lemma mulsupport_LaurentPolynomial (d : ğ•œ â†’ â„¤) :
    (Function.mulSupport fun u z â†¦ (z - u) ^ d u) = d.support := by
  ext u
  constructor
  Â· intro h
    simp_all only [Function.mem_mulSupport, ne_eq, Function.mem_support]
    by_contra hCon
    simp only [hCon, zpow_zero] at h
    tauto
  Â· intro h
    simp only [Function.mem_mulSupport, ne_eq]
    by_contra hCon
    have := congrFun hCon u
    simp only [sub_self, Pi.one_apply] at this
    have : (0 : ğ•œ) ^ d u â‰  0 := ne_zero_of_eq_one this
    rw [zpow_ne_zero_iff h] at this
    tauto

/-- The order of the Laurent polynomial `(âˆá¶  u, fun z â†¦ (z - u) ^ d u)` at z equals `d z`. -/
theorem order_LaurentPolynomial {z : ğ•œ} (d : ğ•œ â†’ â„¤) (hâ‚d : Set.Finite d.support) :
    (((meromorphicNF_LaurentPolynomial d).meromorphicOn) z trivial).order = d z := by
  rw [MeromorphicAt.order_eq_int_iff]
  use âˆ x âˆˆ hâ‚d.toFinset.erase z, fun z => (z - x) ^ d x,
    analyticAt_finLaurentPolynomial_off_support d (hâ‚d.toFinset.erase z)
      (Finset.not_mem_erase z hâ‚d.toFinset)
  constructor
  Â· simp only [Finset.prod_apply]
    rw [Finset.prod_ne_zero_iff]
    intro u hu
    apply zpow_ne_zero
    rw [sub_ne_zero]
    by_contra hCon
    rw [hCon] at hu
    have := Finset.not_mem_erase u hâ‚d.toFinset
    tauto
  Â· apply Filter.Eventually.of_forall
    intro x
    have tâ‚€ : (Function.mulSupport fun u z => (z - u) ^ d u).Finite := by
      rwa [mulsupport_LaurentPolynomial d]
    have tâ‚ : hâ‚d.toFinset = tâ‚€.toFinset := by
      simp [eq_comm, mulsupport_LaurentPolynomial d]
    rw [finprod_eq_prod _ tâ‚€, tâ‚, eq_comm]
    simp only [Finset.prod_apply, smul_eq_mul]
    have : z âˆ‰ hâ‚d.toFinset.erase z := Finset.not_mem_erase z hâ‚d.toFinset
    by_cases hz : z âˆˆ hâ‚d.toFinset
    Â· rw [tâ‚] at hz
      simp_rw [â† Finset.mul_prod_erase tâ‚€.toFinset _ hz]
    Â· have : tâ‚€.toFinset = tâ‚€.toFinset.erase z := by
        rw [eq_comm]
        apply Finset.erase_eq_of_not_mem
        rwa [tâ‚] at hz
      rw [this]
      have : (x - z) ^ d z = 1 := by
        simp only [Set.Finite.mem_toFinset, Function.mem_support, ne_eq, Decidable.not_not] at hz
        simp [hz]
      rw [this]
      simp

/--
Laurent polynomials are nowhere locally constant zero.
-/
theorem order_LaurentPolynomial_ne_top {z : ğ•œ} (d : ğ•œ â†’ â„¤) :
    (meromorphicNF_LaurentPolynomial d (trivial : z âˆˆ âŠ¤)).meromorphicAt.order â‰  âŠ¤ := by
  by_cases hd : Set.Finite (Function.support d)
  Â· simp [order_LaurentPolynomial d hd]
  Â· rw [â† mulsupport_LaurentPolynomial] at hd
    have : AnalyticAt ğ•œ (1 : ğ•œ â†’ ğ•œ) z := analyticAt_const
    simp [finprod_of_infinite_mulSupport hd, this.meromorphicAt_order,
      this.analyticOrderAt_eq_zero.2 (by simp)]

/--
The divisor function associated with the divisor of the Laurent polynomial
`(âˆá¶  u, fun z â†¦ (z - u) ^ d u)` equals `d`.
-/
theorem divisor_LaurentPolynomial [CompleteSpace ğ•œ] (d : ğ•œ â†’ â„¤)
  (hâ‚d : Set.Finite d.support) :
  MeromorphicOn.divisor (âˆá¶  u, fun z â†¦ (z - u) ^ d u) âŠ¤ = d := by
  ext z
  simp_rw [MeromorphicOn.divisor_apply (meromorphicNF_LaurentPolynomial d).meromorphicOn
    (by simp : z âˆˆ Set.univ)]
  rw [order_LaurentPolynomial d hâ‚d]
  simp

/--
If `D` is a divisor, then the function associated with the divisor of the
Laurent polynomial equals `D`.
-/
theorem divisor_LaurentPolynomial_within [CompleteSpace ğ•œ] {U : Set ğ•œ}
    (D : Function.locallyFinsuppWithin U â„¤) (hD : Set.Finite D.support) :
    MeromorphicOn.divisor (âˆá¶  u, fun z â†¦ (z - u) ^ D u) U = D := by
  ext z
  by_cases hz : z âˆˆ U
  Â· simp [(MeromorphicNFOn_LaurentPolynomial D U).meromorphicOn, hz,
      MeromorphicOn.divisor_apply, order_LaurentPolynomial D hD]
  Â· simp [hz]
