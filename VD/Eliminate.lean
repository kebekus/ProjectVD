import Mathlib.Analysis.SpecialFunctions.Log.NegMulLog
import VD.ToMathlib.FactorizedRational
import VD.ToMathlib.Congruence_Divisor
-- -----------------

/--
Any set is codiscrete within itself.
-/
theorem Filter.codiscreteWithin_self {X : Type*} [TopologicalSpace X] (U : Set X) :
    U âˆˆ Filter.codiscreteWithin U := by simp [mem_codiscreteWithin]

-- -----------------

/--
If `X` is a `T1Space`, then functions with locally finite support within `U`
have discrete support within `U`.
-/
theorem Function.locallyFinsuppWithin.supportDiscreteWithinDomain
    {X : Type*} [TopologicalSpace X] [T1Space X] (U : Set X)
    {Y : Type*} [Zero Y]
    (f : Function.locallyFinsuppWithin U Y) :
    f =á¶ [Filter.codiscreteWithin U] 0 := by
  apply codiscreteWithin_iff_locallyFiniteComplementWithin.2
  have : f.support = (U \ {x | f x = (0 : X â†’ Y) x}) := by
    ext x
    simp only [mem_support, ne_eq, Pi.zero_apply, Set.mem_diff, Set.mem_setOf_eq, iff_and_self]
    exact (support_subset_iff.1 f.supportWithinDomain) x
  rw [â† this]
  exact f.supportLocallyFiniteWithinDomain

-- -----------------

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {U : Set ğ•œ}
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

open Filter Real

/-!
## Extration of Zeros and Poles
-/

/--
If `f` is meromorphic on an open set `U`, if `f` is nowhere locally constant
zero, and if the support of the divisor of `f` is finite, then there exists an
analytic function `g` on `U` without zeros such that `f` is equivalent, modulo
equality on a codiscrete set, to the product of `g` and the factorized rational
function associated with the divisor of `f.
-/
theorem MeromorphicOn.extract_zeros_poles [CompleteSpace ğ•œ] [CompleteSpace E] {f : ğ•œ â†’ E}
    (hâ‚f : MeromorphicOn f U) (hâ‚‚f : âˆ€ u : U, (hâ‚f u u.2).order â‰  âŠ¤)
    (hâ‚ƒf : (divisor f U).support.Finite) :
    âˆƒ g : ğ•œ â†’ E, AnalyticOnNhd ğ•œ g U âˆ§ (âˆ€ u : U, g u â‰  0) âˆ§
      f =á¶ [codiscreteWithin U] (âˆá¶  u, (Â· - u) ^ divisor f U u) â€¢ g := by
  -- Take `g` as the inverse of the Laurent polynomial defined below, converted
  -- to a meromorphic function in normal form. Then check all the properties.
  let Ï† := âˆá¶  u, (Â· - u) ^ (divisor f U u)
  have hÏ† : MeromorphicOn Ï† U := (Function.FactorizedRational.meromorphicNFOn (divisor f U) U).meromorphicOn
  let g := toMeromorphicNFOn (Ï†â»Â¹ â€¢ f) U
  have hg : MeromorphicNFOn g U := by apply meromorphicNFOn_toMeromorphicNFOn
  refine âŸ¨g, ?_, ?_, ?_âŸ©
  Â· -- AnalyticOnNhd ğ•œ g U
    rw [â† hg.divisor_nonneg_iff_analyticOnNhd, divisor_of_toMeromorphicNFOn (hÏ†.inv.smul hâ‚f),
      divisor_smul hÏ†.inv hâ‚f _ (fun z hz â†¦ hâ‚‚f âŸ¨z, hzâŸ©), divisor_inv,
      Function.FactorizedRational.divisor _ hâ‚ƒf, neg_add_cancel]
    intro z hz
    simp [(hÏ† z hz).order_inv, Function.FactorizedRational.order_ne_top (divisor f U)]
  Â· -- âˆ€ (u : â†‘U), g â†‘u â‰  0
    intro âŸ¨u, huâŸ©
    rw [â† (hg hu).order_eq_zero_iff, â† ((hÏ†.inv.smul hâ‚f) u hu).order_congr
      (toMeromorphicNFOn_eq_self_on_nhdNE (hÏ†.inv.smul hâ‚f) hu).symm]
    rw [(hÏ† u hu).inv.order_smul (hâ‚f u hu), (hÏ† u hu).order_inv, Function.FactorizedRational.order _ hâ‚ƒf]
    simp only [Pi.neg_apply, hâ‚f, hu, divisor_apply, WithTop.LinearOrderedAddCommGroup.coe_neg]
    lift (hâ‚f u hu).order to â„¤ using (hâ‚‚f âŸ¨u, huâŸ©) with n hn
    rw [WithTop.untopâ‚€_coe, (by rfl : -â†‘(n : WithTop â„¤) = (â†‘(-n) : WithTop â„¤)), â† WithTop.coe_add]
    simp
  Â· -- f =á¶ [codiscreteWithin U] (âˆá¶  (u : ğ•œ), fun z â†¦ (z - u) ^ (divisor f U) u) * g
    filter_upwards [(divisor f U).supportDiscreteWithinDomain,
      (hÏ†.inv.smul hâ‚f).meromorphicNFAt_mem_codiscreteWithin,
      codiscreteWithin_self U] with a hâ‚‚a hâ‚ƒa hâ‚„a
    unfold g
    simp only [Pi.smul_apply', toMeromorphicNFOn_eq_toMeromorphicNFAt (hÏ†.inv.smul hâ‚f) hâ‚„a,
      toMeromorphicNFAt_eq_self.2 hâ‚ƒa, Pi.inv_apply]
    rw [â† smul_assoc, smul_eq_mul, mul_inv_cancelâ‚€ _, one_smul]
    rwa [â† ((Function.FactorizedRational.meromorphicNFOn_univ (divisor f U)) trivial).order_eq_zero_iff,
      Function.FactorizedRational.order, hâ‚‚a, Pi.zero_apply, WithTop.coe_zero]

/--
In the setting of `MeromorphicOn.extract_zeros_poles`, the function `log â€–fâ€–` is
equivalent, modulo equality on codiscrete subsets of `U`, to `âˆ‘á¶  u, (divisor f U
u * log â€–Â· - uâ€–) + log â€–g Â·â€–`.
-/
theorem MeromorphicOn.extract_zeros_poles_log [CompleteSpace ğ•œ] {f g : ğ•œ â†’ E}
    {D : Function.locallyFinsuppWithin U â„¤} (hg : âˆ€ u : U, g u â‰  0)
    (h : f =á¶ [codiscreteWithin U] (âˆá¶  u, (Â· - u) ^ D u) â€¢ g) :
    (log â€–f Â·â€–) =á¶ [codiscreteWithin U] âˆ‘á¶  u, (D u * log â€–Â· - uâ€–) + (log â€–g Â·â€–) := by
  -- Identify support of the sum in the goal
  have tâ‚ : (fun u â†¦ (D u * log â€–Â· - uâ€–)).support = D.support := by
    ext u
    rw [â† not_iff_not]
    simp only [Function.mem_mulSupport, ne_eq, not_not, Function.mem_support, Decidable.not_not]
    constructor <;> intro hx
    Â· obtain âŸ¨y, hyâŸ© := NormedField.exists_one_lt_norm ğ•œ
      have := congrFun hx (y + u)
      simp only [add_sub_cancel_right, Pi.zero_apply, mul_eq_zero, Int.cast_eq_zero, log_eq_zero,
        norm_eq_zero] at this
      rcases this with h | h | h | h
      Â· assumption
      Â· simp only [h, norm_zero] at hy
        linarith
      Â· simp only [h, lt_self_iff_false] at hy
      Â· simp only [h, lt_neg_self_iff] at hy
        linarith
    Â· simp_all only [ne_eq, Subtype.forall, Int.cast_zero, zero_mul]
      rfl
  -- Trivial case: the support of D is infinite
  by_cases hâ‚ƒf : Â¬D.support.Finite
  Â· rw [finsum_of_infinite_support (by simpa [tâ‚] using hâ‚ƒf)]
    rw [finprod_of_infinite_mulSupport
      (by simpa [Function.FactorizedRational.mulSupport] using hâ‚ƒf)] at h
    filter_upwards [h] with x hx
    simp [hx]
  rw [not_not] at hâ‚ƒf
  -- General case
  filter_upwards [h, D.supportDiscreteWithinDomain,
    codiscreteWithin_self U] with z hz hâ‚‚z hâ‚ƒz
  rw [hz, finprod_eq_prod_of_mulSupport_subset (s := hâ‚ƒf.toFinset) _
    (by simp_all [Function.FactorizedRational.mulSupport]),
    finsum_eq_sum_of_support_subset (s := hâ‚ƒf.toFinset) _ (by simp_all)]
  have : âˆ€ x âˆˆ hâ‚ƒf.toFinset, â€–z - xâ€– ^ D x â‰  0 := by
    intro x hx
    rw [Set.Finite.mem_toFinset, Function.mem_support, ne_eq] at hx
    rw [ne_eq, zpow_eq_zero_iff hx, norm_eq_zero]
    by_contra hCon
    rw [sub_eq_zero] at hCon
    rw [hCon] at hâ‚‚z
    tauto
  simp only [Pi.smul_apply', Finset.prod_apply, Pi.pow_apply, norm_smul, norm_prod, norm_zpow]
  rw [log_mul (Finset.prod_ne_zero_iff.2 this) (by simp [hg âŸ¨z, hâ‚ƒzâŸ©]), log_prod _ _ this]
  simp [log_zpow]
