import Mathlib.Algebra.Order.Group.PosPart
import Mathlib.Analysis.Complex.ValueDistribution.CountingFunction
import Mathlib.RingTheory.LocalRing.Basic
import VD.MathlibSubmitted.DivisorOrder

open MeromorphicOn Metric Real Set Classical Function.locallyFinsuppWithin

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {U : Set ğ•œ} {f g : ğ•œ â†’ E} {a : WithTop E} {aâ‚€ : E}


namespace MeromorphicOn

/--
The order of a constant function is `âŠ¤` if the the constant is zero and `0` otherwise.
-/
theorem meromorphicOrderAt_const (zâ‚€ : ğ•œ) (e : E) :
    meromorphicOrderAt (fun _ â†¦ e) zâ‚€ = if e = 0 then âŠ¤ else (0 : WithTop â„¤) := by
  by_cases he : e = 0
  Â· simp [he, meromorphicOrderAt_eq_top_iff]
  simp [he]
  rw [(by rfl : (0 : WithTop â„¤) = (0 : â„¤)), meromorphicOrderAt_eq_int_iff (MeromorphicAt.const e zâ‚€)]
  use fun _ â†¦ e
  simp [he]
  fun_prop

/--
Variant of `meromorphicOrderAt_const`, for constant functions defined by
coercion from natural numbers.
-/
theorem meromorphicOrderAt_const_ofNat (zâ‚€ : ğ•œ) (n : â„¤) :
    meromorphicOrderAt (n : ğ•œ â†’ ğ•œ) zâ‚€ = if (n : ğ•œ) = 0 then âŠ¤ else (0 : WithTop â„¤) := by
  apply meromorphicOrderAt_const


/--
The divisor of a constant function is `0`.
-/
@[simp]
theorem divisor_const (e : E) :
    divisor (fun _ â†¦ e) U = 0 := by
  classical
  ext x
  simp only [divisor_def, meromorphicOrderAt_const, Function.locallyFinsuppWithin.coe_zero,
    Pi.zero_apply, ite_eq_right_iff, WithTop.untopâ‚€_eq_zero,
    LinearOrderedAddCommGroupWithTop.top_ne_zero, imp_false, ite_eq_left_iff, WithTop.zero_ne_top,
    Decidable.not_not, and_imp]
  tauto

/--
The divisor of a constant function is `0`.
-/
@[simp]
theorem divisor_intCast (n : â„¤) :
    divisor (n : ğ•œ â†’ ğ•œ) U = 0 := divisor_const (n : ğ•œ)

/--
The divisor of a constant function is `0`.
-/
@[simp]
theorem divisor_natCast (n : â„•) :
    divisor (n : ğ•œ â†’ ğ•œ) U = 0 := divisor_const (n : ğ•œ)

/--
The divisor of a constant function is `0`.
-/
@[simp] theorem divisor_ofNat (n : â„•) :
    divisor (ofNat(n) : ğ•œ â†’ ğ•œ) U = 0 := by
  convert divisor_const (n : ğ•œ)
  simp [Semiring.toGrindSemiring_ofNat ğ•œ n]

/--
If `f` is meromorphic, then the divisor of `f ^ n` is `n` times the divisor of `f`.
-/
theorem divisor_pow {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicOn f U) (n : â„•) :
    divisor (f ^ n) U = n â€¢ divisor f U := by
  ext z
  by_cases hn : n = 0
  Â· simp only [hn, pow_zero, divisor_def, zero_nsmul, Function.locallyFinsuppWithin.coe_zero,
      Pi.zero_apply, ite_eq_right_iff, WithTop.untopâ‚€_eq_zero, and_imp]
    intro _ _
    have := meromorphicOrderAt_const_ofNat z 1
    simp_all
  by_cases hz : Â¬z âˆˆ U
  Â· simp [hz]
  simp_all only [Decidable.not_not, hf.pow n, divisor_apply,
    Function.locallyFinsuppWithin.coe_nsmul, Pi.smul_apply, Int.nsmul_eq_mul]
  rw [meromorphicOrderAt_pow (hf z hz)]
  aesop

/--
If `f` is meromorphic, then the divisor of `f ^ n` is `n` times the divisor of `f`.
-/
theorem divisor_fun_pow {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicOn f U) (n : â„•) :
    divisor (fun z â†¦ f z ^ n) U = n â€¢ divisor f U := divisor_pow hf n

/--
If `f` is meromorphic, then the divisor of `f ^ n` is `n` times the divisor of `f`.
-/
theorem divisor_zpow {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicOn f U) (n : â„¤) :
    divisor (f ^ n) U = n â€¢ divisor f U := by
  ext z
  by_cases hn : n = 0
  Â· simp only [hn, zpow_zero, divisor_def, zero_smul, Function.locallyFinsuppWithin.coe_zero,
      Pi.zero_apply, ite_eq_right_iff, WithTop.untopâ‚€_eq_zero, and_imp]
    intro _ _
    have := meromorphicOrderAt_const_ofNat z 1
    simp_all
  by_cases hz : Â¬z âˆˆ U
  Â· simp [hz]
  simp_all only [Decidable.not_not, hf.zpow n, divisor_apply,
    Function.locallyFinsuppWithin.coe_zsmul, Pi.smul_apply, Int.zsmul_eq_mul]
  rw [meromorphicOrderAt_zpow (hf z hz)]
  aesop

/--
If `f` is meromorphic, then the divisor of `f ^ n` is `n` times the divisor of `f`.
-/
theorem divisor_fun_zpow {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicOn f U) (n : â„¤) :
    divisor (fun z â†¦ f z ^ n) U = n â€¢ divisor f U := divisor_zpow hf n

end MeromorphicOn

namespace Function.locallyFinsuppWithin

noncomputable def logCounting' {E : Type*} [NormedAddCommGroup E] [ProperSpace E] :
    locallyFinsuppWithin (univ : Set E) â„¤ â†’â‚—[â„¤] (â„ â†’ â„) where
  toFun D := fun r â†¦ âˆ‘á¶  z, D.toClosedBall r z * log (r * â€–zâ€–â»Â¹) + (D 0) * log r
  map_add' Dâ‚ Dâ‚‚ := by
    simp only [map_add, coe_add, Pi.add_apply, Int.cast_add]
    ext r
    have {A B C D : â„} : A + B + (C + D) = A + C + (B + D) := by ring
    rw [Pi.add_apply, this]
    congr 1
    Â· have hâ‚s : ((Dâ‚.toClosedBall r).support âˆª (Dâ‚‚.toClosedBall r).support).Finite := by
        apply Set.finite_union.2
        constructor
        <;> apply finiteSupport _ (isCompact_closedBall 0 |r|)
      repeat
        rw [finsum_eq_sum_of_support_subset (s := hâ‚s.toFinset)]
        try simp_rw [â† Finset.sum_add_distrib, â† add_mul]
      repeat
        intro x hx
        by_contra
        simp_all
    Â· ring
  map_smul' n D := by
    simp only [map_zsmul, coe_zsmul, Pi.smul_apply, eq_intCast, Int.cast_eq]
    ext r
    rw [Pi.smul_apply, smul_add, smul_finsum]
    congr 1
    Â· congr 1
      ext z
      rw [smul_eq_mul, Int.cast_mul]
      ring
    Â· rw [smul_eq_mul, Int.cast_mul]
      ring

end Function.locallyFinsuppWithin

namespace ValueDistribution

variable [ProperSpace ğ•œ]

variable (f a) in
noncomputable def logCounting' : â„ â†’ â„ := by
  by_cases h : a = âŠ¤
  Â· exact (divisor f univ)â».logCounting'
  Â· exact (divisor (fun z â†¦ f z - a.untopâ‚€) univ)âº.logCounting'

/--
For natural numbers `n`, the counting function counting zeros of `f ^ n` equals
`n` times the counting function counting zeros of `f`.
-/
@[simp] theorem logCounting_pow_zero {f : ğ•œ â†’ ğ•œ} {n : â„•} (hf : MeromorphicOn f Set.univ) :
    logCounting' (f ^ n) 0 = n â€¢ logCounting' f 0 := by
  simp [logCounting', divisor_fun_pow hf n, â† nsmul_posPart]

/--
For natural numbers `n`, the counting function counting poles of `f ^ n` equals
`n` times the counting function counting poles of `f`.
-/
@[simp] theorem logCounting_pow_top {f : ğ•œ â†’ ğ•œ} {n : â„•} (hf : MeromorphicOn f Set.univ) :
    logCounting' (f ^ n) âŠ¤ = n â€¢ logCounting' f âŠ¤ := by
  simp [logCounting', divisor_pow hf n, â† nsmul_negPart]

end ValueDistribution
