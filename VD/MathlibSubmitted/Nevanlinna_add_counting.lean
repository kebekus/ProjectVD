import Mathlib.Analysis.Complex.ValueDistribution.CountingFunction

open MeromorphicOn Metric Real Set Classical

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {U : Set ğ•œ} {f g : ğ•œ â†’ E} {a : WithTop E} {aâ‚€ : E}

/--
Adding a locally vanishing function does not change the order.
-/
theorem meromorphicOrderAt_add_top
    {fâ‚ fâ‚‚ : ğ•œ â†’ E} {x : ğ•œ} (hfâ‚ : meromorphicOrderAt fâ‚ x = âŠ¤) :
    meromorphicOrderAt (fâ‚ + fâ‚‚) x = meromorphicOrderAt fâ‚‚ x := by
  rw [meromorphicOrderAt_congr]
  filter_upwards [meromorphicOrderAt_eq_top_iff.1 hfâ‚] with z hz
  simp_all

namespace ValueDistribution

/--
The counting function of a constant function is zero.
-/
@[simp] theorem logCounting_const
    {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [ProperSpace ğ•œ]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {c : E} {e : WithTop E} :
    logCounting (fun _ â†¦ c : ğ•œ â†’ E) e = 0 := by
  simp [logCounting]

/--
The counting function of the constant function zero is zero.
-/
@[simp] theorem logCounting_const_zero [ProperSpace ğ•œ] {e : WithTop E} :
    logCounting (0 : ğ•œ â†’ E) e = 0 := logCounting_const

/--
The divisor of `fâ‚ + fâ‚‚` is larger than or equal to the minimum of the divisors
of `fâ‚` and `fâ‚‚`, respectively.
-/
theorem min_divisor_le_divisor_add {fâ‚ fâ‚‚ : ğ•œ â†’ E} {z : ğ•œ} {U : Set ğ•œ} (hfâ‚ : MeromorphicOn fâ‚ U)
    (hfâ‚‚ : MeromorphicOn fâ‚‚ U) (hâ‚z : z âˆˆ U) (hâ‚ƒ : meromorphicOrderAt (fâ‚ + fâ‚‚) z â‰  âŠ¤) :
    min (divisor fâ‚ U z) (divisor fâ‚‚ U z) â‰¤ divisor (fâ‚ + fâ‚‚) U z := by
  by_cases hz : z âˆ‰ U
  Â· simp_all
  simp only [Decidable.not_not] at hz
  rw [divisor_apply hfâ‚ hz, divisor_apply hfâ‚‚ hz, divisor_apply (hfâ‚.add hfâ‚‚) hz]
  by_cases hâ‚ : meromorphicOrderAt fâ‚ z = âŠ¤
  Â· rw [inf_le_iff]
    right
    rwa [meromorphicOrderAt_add_top]
  by_cases hâ‚‚ : meromorphicOrderAt fâ‚‚ z = âŠ¤
  Â· rw [inf_le_iff]
    left
    rwa [add_comm, meromorphicOrderAt_add_top]
  rw [â† WithTop.untopâ‚€_min hâ‚ hâ‚‚]
  apply WithTop.untopâ‚€_le_untopâ‚€ hâ‚ƒ
  exact meromorphicOrderAt_add (hfâ‚ z hz) (hfâ‚‚ z hz)

/--
The pole divisor of `fâ‚ + fâ‚‚` is smaller than or equal to the maximum of the
pole divisors of `fâ‚` and `fâ‚‚`, respectively.
-/
theorem negPart_divisor_add_le_max {fâ‚ fâ‚‚ : ğ•œ â†’ E} {U : Set ğ•œ} (hfâ‚ : MeromorphicOn fâ‚ U)
    (hfâ‚‚ : MeromorphicOn fâ‚‚ U) :
    (divisor (fâ‚ + fâ‚‚) U)â» â‰¤ max (divisor fâ‚ U)â» (divisor fâ‚‚ U)â» := by
  intro z
  by_cases hz : z âˆ‰ U
  Â· simp [hz]
  rw [Decidable.not_not] at hz
  simp only [Function.locallyFinsuppWithin.negPart_apply, Function.locallyFinsuppWithin.max_apply]
  by_cases hfâ‚â‚‚ : meromorphicOrderAt (fâ‚ + fâ‚‚) z = âŠ¤
  Â· simp [divisor_apply (hfâ‚.add hfâ‚‚) hz, hfâ‚â‚‚, negPart_nonneg]
  rw [â† negPart_min]
  apply ((le_iff_posPart_negPart _ _).1 (min_divisor_le_divisor_add hfâ‚ hfâ‚‚ hz hfâ‚â‚‚)).2

/--
The pole divisor of `fâ‚ + fâ‚‚` is smaller than or equal to the sum of the pole
divisors of `fâ‚` and `fâ‚‚`, respectively.
-/
theorem negPart_divisor_add_le_add {fâ‚ fâ‚‚ : ğ•œ â†’ E} {U : Set ğ•œ} (hfâ‚ : MeromorphicOn fâ‚ U)
    (hfâ‚‚ : MeromorphicOn fâ‚‚ U) :
    (divisor (fâ‚ + fâ‚‚) U)â» â‰¤ (divisor fâ‚ U)â» + (divisor fâ‚‚ U)â» := by
  calc (divisor (fâ‚ + fâ‚‚) U)â»
  _ â‰¤ max (divisor fâ‚ U)â» (divisor fâ‚‚ U)â» :=
    negPart_divisor_add_le_max hfâ‚ hfâ‚‚
  _ â‰¤ (divisor fâ‚ U)â» + (divisor fâ‚‚ U)â» := by
    by_cases h : (divisor fâ‚ U)â» â‰¤ (divisor fâ‚‚ U)â»
    <;> simp_all [negPart_nonneg]

/--
For `1 â‰¤ r`, the counting function of `f + g` at `âŠ¤` is less than or equal to
the sum of the counting functions of `f` and `g`, respectively.
-/
theorem counting_top_add_le [ProperSpace ğ•œ] {fâ‚ fâ‚‚ : ğ•œ â†’ E} {r : â„}
    (hâ‚fâ‚ : MeromorphicOn fâ‚ Set.univ) (hâ‚fâ‚‚ : MeromorphicOn fâ‚‚ Set.univ) (hr : 1 â‰¤ r) :
    logCounting (fâ‚ + fâ‚‚) âŠ¤ r â‰¤ ((logCounting fâ‚ âŠ¤) + (logCounting fâ‚‚ âŠ¤)) r := by
  simp only [logCounting, â†“reduceDIte]
  rw [â† Function.locallyFinsuppWithin.logCounting.map_add]
  exact Function.locallyFinsuppWithin.logCounting_le (negPart_divisor_add_le_add hâ‚fâ‚ hâ‚fâ‚‚) hr

/--
Asymptotically, the counting function of `f + g` at `âŠ¤` is less than or equal to
the sum of the counting functions of `f` and `g`, respectively.
-/
theorem counting_top_add_eventually_le [ProperSpace ğ•œ] {fâ‚ fâ‚‚ : ğ•œ â†’ E}
    (hâ‚fâ‚ : MeromorphicOn fâ‚ Set.univ) (hâ‚fâ‚‚ : MeromorphicOn fâ‚‚ Set.univ) :
    logCounting (fâ‚ + fâ‚‚) âŠ¤ â‰¤á¶ [Filter.atTop] (logCounting fâ‚ âŠ¤) + (logCounting fâ‚‚ âŠ¤) := by
  filter_upwards [Filter.eventually_ge_atTop 1]
  exact fun _ hr â†¦ counting_top_add_le hâ‚fâ‚ hâ‚fâ‚‚ hr

/--
For `1 â‰¤ r`, the counting function of a sum `âˆ‘ a, f a` at `âŠ¤` is less than or
equal to the sum of the counting functions of `f Â·`.
-/
theorem counting_top_sum_le [ProperSpace ğ•œ] {Î± : Type*} (s : Finset Î±) (f : Î± â†’ ğ•œ â†’ E)
    {r : â„} (hâ‚f : âˆ€ a, MeromorphicOn (f a) Set.univ) (hr : 1 â‰¤ r) :
    logCounting (âˆ‘ a âˆˆ s, f a) âŠ¤ r â‰¤ (âˆ‘ a âˆˆ s, (logCounting (f a) âŠ¤)) r := by
  induction s using Finset.induction with
  | empty =>
    simp
  | insert a s ha hs =>
    rw [Finset.sum_insert ha, Finset.sum_insert ha]
    calc logCounting (f a + âˆ‘ x âˆˆ s, f x) âŠ¤ r
    _ â‰¤ (logCounting (f a) âŠ¤ + logCounting (âˆ‘ x âˆˆ s, f x) âŠ¤) r :=
      counting_top_add_le (hâ‚f a) (MeromorphicOn.sum hâ‚f) hr
    _ â‰¤ (logCounting (f a) âŠ¤ + âˆ‘ x âˆˆ s, logCounting (f x) âŠ¤) r :=
      add_le_add (by trivial) hs

/--
Asymptotically, the counting function of a sum `âˆ‘ a, f a` at `âŠ¤` is less than or
equal to the sum of the counting functions of `f Â·`.
-/
theorem counting_top_sum_eventually_le [ProperSpace ğ•œ] {Î± : Type*} (s : Finset Î±) (f : Î± â†’ ğ•œ â†’ E)
    (hâ‚f : âˆ€ a, MeromorphicOn (f a) Set.univ) :
    logCounting (âˆ‘ a âˆˆ s, f a) âŠ¤ â‰¤á¶ [Filter.atTop] âˆ‘ a âˆˆ s, (logCounting (f a) âŠ¤) := by
  filter_upwards [Filter.eventually_ge_atTop 1]
  exact fun _ hr â†¦ counting_top_sum_le s f hâ‚f hr

end ValueDistribution
