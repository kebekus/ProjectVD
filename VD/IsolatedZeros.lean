import Mathlib.Analysis.SpecialFunctions.Log.Basic
import Mathlib.Analysis.Meromorphic.FactorizedRational
import VD.ToMathlib.LeadCoefficient
import VD.ToMathlib.Eliminate
import VD.ToMathlib.FinprodMeromorphic

open Classical Function Function.FactorizedRational MeromorphicAt MeromorphicOn Real Topology

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-!
## Theorems concerning MeromorphicAt
-/

theorem MeromorphicAt.frequently_zero_iff_eventually_zero {f : ğ•œ â†’ E} {x : ğ•œ}
    (hf : MeromorphicAt f x) :
    (âˆƒá¶  z in ğ“[â‰ ] x, f z = 0) â†” f =á¶ [ğ“[â‰ ] x] 0 :=
  âŸ¨hf.eventually_eq_zero_or_eventually_ne_zero.resolve_right,
    fun h â†¦ h.frequentlyâŸ©

/--
Variant of the principle of isolated zeros: Let `U` be a subset of `ğ•œ` and
assume that `x âˆˆ U` is not an isolated point of `U`. If a function `f` is
meromorphic at `x` and vanishes along a subset that is codiscrete within `U`,
then `f` vanishes in a punctured neighbourhood of `f`.

For a typical application, let `U` be a closed ball and let `x` be a point on
the circumference. If `f` is meromorphic at `x` and vanishes on `U`, then it
will vanish in a punctured neighbourhood of `x`, which intersects `U`
non-trivally but is not contained in `U`.

The assumption that `x` is not an isolated point of `U` is expressed in `hâ‚‚x` as
`Uá¶œ âˆ‰ ğ“[â‰ ] x`.
-/
theorem MeromorphicAt.eventuallyEq_zero_nhdNE_of_eventuallyEq_zero_codiscreteWithin
    {U : Set ğ•œ} {x : ğ•œ} {f : ğ•œ â†’ E}
    (hf : MeromorphicAt f x)
    (hâ‚x : x âˆˆ U)
    (hâ‚‚x : Uá¶œ âˆ‰ ğ“[â‰ ] x)
    (h : f =á¶ [Filter.codiscreteWithin U] 0) :
    f =á¶ [ğ“[â‰ ] x] 0 := by
  rw [â† (hf).frequently_zero_iff_eventually_zero]
  by_contra hCon
  rw [Filter.EventuallyEq, Filter.Eventually, mem_codiscreteWithin] at h
  have := h x hâ‚x
  simp only [Pi.zero_apply, Filter.disjoint_principal_right, Set.compl_diff] at this
  have := Filter.inter_mem (Filter.not_frequently.1 hCon) this
  simp_all [Set.inter_union_distrib_left, (by tauto_set : {x | Â¬f x = 0} âˆ© {x | f x = 0} = âˆ…)]

/--
Variant of the principle of isolated zeros: Let `U` be a subset of `ğ•œ` and
assume that `x âˆˆ U` is not an isolated point of `U`. If a function `f` is
meromorphic at `x` and vanishes along a subset that is codiscrete within `U`,
then `f` vanishes in a punctured neighbourhood of `f`.

For a typical application, let `U` be the closure of the Mandelbrot set and let
`x` be a point in its frontier. If `f` is meromorphic at `x` and vanishes on
`U`, then it will vanish in a punctured neighbourhood of `x`, even though this
neighbourhood is not contained in `U`.
-/
theorem MeromorphicAt.eventuallyEq_nhdNE_of_eventuallyEq_codiscreteWithin
    {U : Set ğ•œ} {x : ğ•œ} {f g : ğ•œ â†’ E}
    (hf : MeromorphicAt f x)
    (hg : MeromorphicAt g x)
    (hâ‚x : x âˆˆ U)
    (hâ‚‚x : Uá¶œ âˆ‰ ğ“[â‰ ] x)
    (h : f =á¶ [Filter.codiscreteWithin U] g) :
    f =á¶ [ğ“[â‰ ] x] g := by
  rw [Filter.eventuallyEq_iff_sub] at *
  apply (hf.sub hg).eventuallyEq_zero_nhdNE_of_eventuallyEq_zero_codiscreteWithin hâ‚x hâ‚‚x h

/-!
## Theorems concerning the Leading Coefficient
-/

theorem leadCoefficient_const {x : ğ•œ} {e : ğ•œ} :
    leadCoefficient (fun _ â†¦ e) x = e := by
  by_cases he : e = 0
  Â· rw [he]
    apply analyticAt_const.meromorphicAt.leadCoefficient_of_order_eq_top
    rw [MeromorphicAt.order_eq_top_iff]
    simp
  Â· exact analyticAt_const.leadCoefficient_of_nonvanish he

theorem leadCoefficient_prod {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ•œ} {x : ğ•œ}
    (h : âˆ€ Ïƒ, MeromorphicAt (f Ïƒ) x) :
    leadCoefficient (âˆ n âˆˆ s, f n) x = âˆ n âˆˆ s, leadCoefficient (f n) x := by
  classical
  apply Finset.induction
    (motive := fun b' â†¦ (leadCoefficient (âˆ n âˆˆ b' , f n) x = âˆ n âˆˆ b', leadCoefficient (f n) x))
  Â· simp only [Finset.univ_eq_empty, Finset.prod_empty, forall_const]
    apply leadCoefficient_const
  Â· intro Ïƒ sâ‚ hÏƒ hind
    rw [Finset.prod_insert hÏƒ, Finset.prod_insert hÏƒ, leadCoefficient_mul (h Ïƒ) (Finset.meromorphicAt_prod h),
      hind]

/-!
## Theorems concerning Affine Functions
-/

theorem meromorphicOn_affine {y : ğ•œ} :
    MeromorphicOn (Â· - y) Set.univ :=
  fun _ _ â†¦ by fun_prop

theorem MeromorphicAt.order_affine {x y : ğ•œ} :
    (meromorphicOn_affine (y := y) x (by tauto)).order â‰  âŠ¤ := by
  rw [MeromorphicAt.order_ne_top_iffâ‚‚]
  apply mem_nhdsWithin.2
  by_cases h : x = y
  Â· use Set.univ
    simp only [isOpen_univ, Set.mem_univ, Set.univ_inter, ne_eq, true_and, h]
    intro z hz
    simp_all [sub_eq_zero]
  Â· use {y}á¶œ
    simp_all only [isOpen_compl_iff, Set.finite_singleton, Set.Finite.isClosed, Set.mem_compl_iff,
      Set.mem_singleton_iff, not_false_eq_true, ne_eq, true_and]
    intro z hz
    simp_all [sub_eq_zero]

theorem leadCoefficient_affine {x y : ğ•œ} :
    leadCoefficient (Â· - y) x = if x = y then 1 else x - y := by
  by_cases h : x = y
  Â· simp_all only [sub_self, ite_true]
    apply AnalyticAt.leadCoefficient_of_order_eq_finiteâ‚ (n := 1) (by fun_prop) (by apply one_ne_zero)
    simp_all
  Â· simp_all only [ite_false]
    apply AnalyticAt.leadCoefficient_of_nonvanish (by fun_prop)
    simp_all [sub_ne_zero]

/-!
## Theorems concerning Factorized Rational Functions
-/

private lemma Function.FactorizedRational.mulSupport_update {d : ğ•œ â†’ â„¤} {x : ğ•œ}
    (h : d.support.Finite) :
    (fun u â†¦ (x - u) ^ Function.update d x 0 u).mulSupport âŠ† h.toFinset := by
  intro u
  contrapose
  intro hu
  simp_all
  by_cases hâ‚ : u = x
  Â· rw [hâ‚]
    simp
  Â· simp_all

theorem Function.FactorizedRational.leadCoefficient {d : ğ•œ â†’ â„¤} {x : ğ•œ} (h : d.support.Finite) :
    leadCoefficient (âˆá¶  u, (Â· - u) ^ d u) x = âˆá¶  u, (x - u) ^ update d x 0 u := by
  have : (fun u â†¦ (Â· - u) ^ d u).mulSupport âŠ† h.toFinset := by
    simp [Function.FactorizedRational.mulSupport]
  rw [finprod_eq_prod_of_mulSupport_subset _ this, leadCoefficient_prod (fun _ â†¦ by fun_prop),
    finprod_eq_prod_of_mulSupport_subset _ (mulSupport_update h)]
  apply Finset.prod_congr rfl
  intro y hy
  rw [leadCoefficient_zpowâ‚ (by fun_prop) (by simp [MeromorphicAt.order_affine])]
  by_cases hxy : x = y
  Â· rw [hxy, leadCoefficient_affine]
    simp_all
  Â· rw [leadCoefficient_affine]
    simp only [hxy, reduceIte]
    congr
    apply (Function.update_of_ne (by tauto) _ _).symm

theorem Function.FactorizedRational.leadCoefficient_off_support {d : ğ•œ â†’ â„¤} {x : ğ•œ}
    (hâ‚ : d.support.Finite) (hâ‚‚ : x âˆ‰ d.support) :
    MeromorphicAt.leadCoefficient (âˆá¶  u, (Â· - u) ^ d u) x = âˆá¶  u, (x - u) ^ d u := by
  rw [leadCoefficient hâ‚, finprod_eq_prod_of_mulSupport_subset _ (mulSupport_update hâ‚)]
  have : (fun u â†¦ (x - u) ^ d u).mulSupport âŠ† hâ‚.toFinset := by
    intro u
    contrapose
    simp_all
  rw [finprod_eq_prod_of_mulSupport_subset _ this, Finset.prod_congr rfl]
  intro y hy
  congr
  apply Function.update_of_ne
  by_contra hCon
  simp_all

theorem log_norm_leadCoefficient {d : ğ•œ â†’ â„¤} {x : ğ•œ} (h : d.support.Finite) :
    log â€–leadCoefficient (âˆá¶  u, (Â· - u) ^ d u) xâ€– = âˆ‘á¶  u, (d u) * log â€–x - uâ€– := by
  -- Simplify left side
  rw [leadCoefficient h, finprod_eq_prod_of_mulSupport_subset _ (mulSupport_update h)]
  have : âˆ€ y âˆˆ h.toFinset, â€–(x - y) ^ update d x 0 yâ€– â‰  0 := by
    intro y _
    by_cases h : x = y
    Â· rw [h]
      simp_all
    Â· simp_all [update_of_ne (by tauto), zpow_ne_zero, sub_ne_zero]
  rw [norm_prod, log_prod _ _ this]
  -- Simplify right side
  have : (fun u â†¦ (d u) * log â€–x - uâ€–).support âŠ† h.toFinset := by
    intro u
    contrapose
    simp_all
  rw [finsum_eq_sum_of_support_subset _ this]
  -- Prove equality summand by summand
  apply Finset.sum_congr rfl
  intro y hy
  rw [norm_zpow, Real.log_zpow]
  by_cases h : x = y
  Â· simp [h]
  Â· rw [Function.update_of_ne (by tauto)]

/-!
# Special Terms in Elimination
-/

theorem MeromorphicOn.extract_zeros_poles_leadCoefficient
    {U : Set ğ•œ} {x : ğ•œ} {f g : ğ•œ â†’ E}
    {D : Function.locallyFinsuppWithin U â„¤}
    (hD : D.support.Finite)
    (hâ‚x : x âˆˆ U)
    (hâ‚‚x : Uá¶œ âˆ‰ ğ“[â‰ ] x)
    (hâ‚f : MeromorphicAt f x)
    (hâ‚g : AnalyticAt ğ•œ g x)
    (hâ‚‚g : g x â‰  0)
    (hâ‚ƒg : f =á¶ [Filter.codiscreteWithin U] (âˆá¶  u, (Â· - u) ^ D u) â€¢ g) :
    leadCoefficient f x = (âˆá¶  u, (x - u) ^ update D x 0 u) â€¢ g x := by
  have tâ‚€ : MeromorphicAt (âˆá¶  u, (Â· - u) ^ D u) x :=
    (FactorizedRational.meromorphicNFOn D U).meromorphicOn x hâ‚x
  rw [leadCoefficient_congr_nhdNE
    (hâ‚f.eventuallyEq_nhdNE_of_eventuallyEq_codiscreteWithin (by fun_prop) hâ‚x hâ‚‚x hâ‚ƒg),
    tâ‚€.leadCoefficient_smul hâ‚g.meromorphicAt,
    hâ‚g.leadCoefficient_of_nonvanish hâ‚‚g]
  simp [Function.FactorizedRational.leadCoefficient hD]

theorem MeromorphicOn.extract_zeros_poles_leadCoefficient_log_norm
    {U : Set ğ•œ} {x : ğ•œ} {f g : ğ•œ â†’ E}
    {D : Function.locallyFinsuppWithin U â„¤}
    (hD : D.support.Finite)
    (hâ‚x : x âˆˆ U)
    (hâ‚‚x : Uá¶œ âˆ‰ ğ“[â‰ ] x)
    (hâ‚f : MeromorphicAt f x)
    (hâ‚g : AnalyticAt ğ•œ g x)
    (hâ‚‚g : g x â‰  0)
    (hâ‚ƒg : f =á¶ [Filter.codiscreteWithin U] (âˆá¶  u, (Â· - u) ^ D u) â€¢ g) :
    log â€–leadCoefficient f xâ€– = âˆ‘á¶  u, (D u) * log â€–x - uâ€– + log â€–g xâ€– := by
  have tâ‚€ : MeromorphicAt ((âˆá¶  u, (Â· - u) ^ D u) â€¢ g) x := by
    apply MeromorphicAt.smul
    apply (FactorizedRational.meromorphicNFOn D U).meromorphicOn x hâ‚x
    apply hâ‚g.meromorphicAt
  have tâ‚ := MeromorphicAt.eventuallyEq_nhdNE_of_eventuallyEq_codiscreteWithin
    hâ‚f tâ‚€ hâ‚x hâ‚‚x hâ‚ƒg
  rw [leadCoefficient_congr_nhdNE tâ‚,
    ((FactorizedRational.meromorphicNFOn D U).meromorphicOn x hâ‚x).leadCoefficient_smul
    hâ‚g.meromorphicAt]
  rw [hâ‚g.leadCoefficient_of_nonvanish hâ‚‚g]
  rw [norm_smul]
  rw [log_mul]
  congr
  apply log_norm_leadCoefficient hD
  --
  simp
  rw [eq_comm]
  apply ((FactorizedRational.meromorphicNFOn D U).meromorphicOn x hâ‚x).zero_ne_leadCoefficient
  apply FactorizedRational.order_ne_top
  --
  simp_all
