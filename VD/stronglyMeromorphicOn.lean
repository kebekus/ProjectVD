import Mathlib.Analysis.Meromorphic.Divisor.MeromorphicFunction
import VD.ToMathlib.MeromorphicNFAt
import VD.Divisor_MeromorphicOn

open Topology

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f : ğ•œ â†’ E}
  {x : ğ•œ}
  {U : Set ğ•œ}


/-- If `f` is meromorphic in normal form, then so is its inverse. -/
theorem MeromorphicNFAt.inv {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicNFAt f x) :
    MeromorphicNFAt fâ»Â¹ x := by
  rcases hf with h | âŸ¨n, g, hâ‚, hâ‚‚, hâ‚ƒâŸ©
  Â· left
    filter_upwards [h] with x hx
    simp [hx]
  Â· right
    use -n, gâ»Â¹, hâ‚.inv hâ‚‚, (by simp_all)
    filter_upwards [hâ‚ƒ] with y hy
    simp only [Pi.inv_apply, hy, Pi.smul_apply', Pi.pow_apply, smul_eq_mul, mul_inv_rev, zpow_neg]
    ring_nf

/-- A function to ğ•œ is meromorphic in normal form at a point iff its inverse is. -/
theorem meromorphicNFAt_iff_meromorphicNFAt_inv {f : ğ•œ â†’ ğ•œ} :
    MeromorphicNFAt f x â†” MeromorphicNFAt fâ»Â¹ x := by
  constructor
  Â· exact MeromorphicNFAt.inv
  Â· nth_rw 2 [â† inv_inv f]
    exact MeromorphicNFAt.inv

/-!
# Normal form of meromorphic functions on a given set

## Definition
-/

/-- A function is 'meromorphic in normal form' on `U` if has normal form at
every point of `U`. -/
def MeromorphicNFOn (f : ğ•œ â†’ E) (U : Set ğ•œ) := âˆ€ z âˆˆ U, MeromorphicNFAt f z

/-!
## Relation to other properties of functions
-/

/-- If a function is meromorphic in normal form on `U`, then it is meromorphic on `U`. -/
theorem MeromorphicNFOn.meromorphicOn (hf : MeromorphicNFOn f U) :
    MeromorphicOn f U := fun z hz â†¦ (hf z hz).meromorphicAt

/-- If a function is meromorphic in normal form on `U`, then its divisor is
non-negative iff it is analytic. -/
theorem MeromorphicNFOn.nonneg_divisor_iff_analyticOnNhd [CompleteSpace E]
    (hâ‚f : MeromorphicNFOn f U) :
    0 â‰¤ MeromorphicOn.divisor f U â†” AnalyticOnNhd ğ•œ f U := by
  constructor <;> intro h
  Â· intro x hx
    rw [â† (hâ‚f x hx).order_nonneg_iff_analyticAt]
    have := h x
    simp only [DivisorOn.coe_zero, Pi.zero_apply, hâ‚f.meromorphicOn, hx,
      MeromorphicOn.divisor_apply, le_refl, implies_true, WithTop.le_untopD_iff,
      WithTop.coe_zero] at this
    assumption
  Â· intro x
    by_cases hx : x âˆˆ U
    Â· simp only [DivisorOn.coe_zero, Pi.zero_apply, hâ‚f.meromorphicOn, hx,
        MeromorphicOn.divisor_apply, le_refl, implies_true, WithTop.le_untopD_iff,
        WithTop.coe_zero]
      exact (hâ‚f x hx).order_nonneg_iff_analyticAt.2 (h x hx)
    Â· simp [hâ‚f.meromorphicOn, hx]

/-- Analytic functions are meromorphic in normal form. -/
theorem AnalyticOnNhd.meromorphicNFOn (hâ‚f : AnalyticOnNhd ğ•œ f U) :
    MeromorphicNFOn f U := fun z hz â†¦ (hâ‚f z hz).meromorphicNFAt

/-!
## Divisors and zeros of meromorphic functions in normal form.
-/

/-- If `f` is meromorphic in normal form on `U` and nowhere locally constant zero, then its
zero set equals the support of the associated divisor. -/
theorem MeromorphicNFOn.zero_set_eq_divisor_support [CompleteSpace E] (hâ‚f : MeromorphicNFOn f U)
    (hâ‚‚f : âˆ€ u : U, (hâ‚f u u.2).meromorphicAt.order â‰  âŠ¤) :
    U âˆ© fâ»Â¹' {0} = (Function.support (MeromorphicOn.divisor f U)) := by
  ext u
  constructor <;> intro hu
  Â· simp_all only [ne_eq, Subtype.exists, exists_prop, Set.mem_inter_iff, Set.mem_preimage,
      Set.mem_singleton_iff, Function.mem_support, hâ‚f.meromorphicOn, MeromorphicOn.divisor_apply,
      WithTop.untopD_eq_self_iff, WithTop.coe_zero, (hâ‚f u hu.1).order_eq_zero_iff,
      not_true_eq_false, false_or]
    exact hâ‚‚f âŸ¨u, hu.1âŸ©
  Â· simp only [Function.mem_support, ne_eq] at hu
    constructor
    Â· exact (MeromorphicOn.divisor f U).supportWithinDomain hu
    Â· rw [Set.mem_preimage, Set.mem_singleton_iff]
      have := (hâ‚f u ((MeromorphicOn.divisor f U).supportWithinDomain hu)).order_eq_zero_iff.not
      simp only [hâ‚f.meromorphicOn, (MeromorphicOn.divisor f U).supportWithinDomain hu,
        MeromorphicOn.divisor_apply, WithTop.untopD_eq_self_iff, WithTop.coe_zero, not_or] at hu
      simp_all [this, hu.1]

/-!
## Criteria to guarantee normal form
-/

/-- If `f` is any function and `g` is analytic without zero on `U`, then `f` is meromorphic in
normal form on `U` iff `g â€¢ f` is meromorphic in normal form on `U`. -/
theorem meromorphicNFOn_smul_iff_right_of_analyticAt {g : ğ•œ â†’ ğ•œ} (hâ‚g : AnalyticOnNhd ğ•œ g U)
    (hâ‚‚g : âˆ€ u : U, g u â‰  0) :
    MeromorphicNFOn (g â€¢ f) U â†” MeromorphicNFOn f U := by
  constructor <;> intro h z hz
  Â· rw [meromorphicNFAt_iff_meromorphicNFAt_of_smul_analytic (hâ‚g z hz) (hâ‚‚g âŸ¨z, hzâŸ©)]
    exact h z hz
  Â· apply MeromorphicNFAt.meromorphicNFAt_of_smul_analytic (h z hz) (hâ‚g z hz)
    exact hâ‚‚g âŸ¨z, hzâŸ©

/-- If `f` is any function and `g` is analytic without zero in `U`, then `f` is meromorphic in
normal form on `U` iff `g * f` is meromorphic in normal form on `U`. -/
theorem meromorphicNFOn_mul_iff_right {f g : ğ•œ â†’ ğ•œ} (hâ‚g : AnalyticOnNhd ğ•œ g U)
    (hâ‚‚g : âˆ€ u : U, g u â‰  0) :
    MeromorphicNFOn (g * f) U â†” MeromorphicNFOn f U := by
  rw [â† smul_eq_mul]
  exact meromorphicNFOn_smul_iff_right_of_analyticAt hâ‚g hâ‚‚g

/-- If `f` is any function and `g` is analytic without zero in `U`, then `f` is meromorphic in
normal form on `U` iff `f * g` is meromorphic in normal form on `U`. -/
theorem meromorphicNFAt_mul_iff_left {f g : ğ•œ â†’ ğ•œ} (hâ‚g : AnalyticOnNhd ğ•œ g U)
    (hâ‚‚g : âˆ€ u : U, g u â‰  0) :
    MeromorphicNFOn (f * g) U â†” MeromorphicNFOn f U := by
  rw [mul_comm, â† smul_eq_mul]
  exact meromorphicNFOn_smul_iff_right_of_analyticAt hâ‚g hâ‚‚g

/-- A function to ğ•œ is meromorphic in normal form on `U` iff its inverse is. -/
theorem meromorphicNFOn_iff_meromorphicNFOn_inv {f : ğ•œ â†’ ğ•œ} :
    MeromorphicNFOn f U â†” MeromorphicNFOn fâ»Â¹ U := by
  constructor
  Â· exact fun h x hx â†¦ meromorphicNFAt_iff_meromorphicNFAt_inv.1 (h x hx)
  Â· exact fun h x hx â†¦ meromorphicNFAt_iff_meromorphicNFAt_inv.2 (h x hx)

/-!
## Continuous extension and conversion to normal form
-/

variable (f U) in
/-- If `f` is meromorphic on `U`, convert `f` to normal form on `U` by changing its values along
a discrete subset within `U`. Otherwise, returns the 0 function. -/
noncomputable def toMeromorphicNFOn :
    ğ•œ â†’ E := by
  by_cases hâ‚f : MeromorphicOn f U
  Â· intro z
    by_cases hz : z âˆˆ U
    Â· exact toMeromorphicNFAt f z z
    Â· exact f z
  Â· exact 0

/-- If `f` is not meromorphic on `U`, conversion to normal form  maps the function to `0`. -/
@[simp] lemma toMeromorphicNFOn_of_not_meromorphicOn (hf : Â¬MeromorphicOn f U) :
    toMeromorphicNFOn f U = 0 := by
  simp [toMeromorphicNFOn, hf]

/-- Conversion to normal form on `U` does not change values outside of `U`. -/
@[simp] lemma toMeromorphicNFOn_eq_self_on_compl (hf : MeromorphicOn f U) :
    Set.EqOn f (toMeromorphicNFOn f U) Uá¶œ := by
  intro x hx
  simp_all [toMeromorphicNFOn]

/-- Conversion to normal form on `U` changes the value only along a discrete subset of `U`. -/
theorem toMeromorphicNFOn_eqOn_codiscrete [CompleteSpace E] (hf : MeromorphicOn f U) :
    f =á¶ [Filter.codiscreteWithin U] toMeromorphicNFOn f U := by
  have : U âˆˆ Filter.codiscreteWithin U := by
    simp [mem_codiscreteWithin.2]
  filter_upwards [hf.analyticAt_codiscreteWithin, this] with a hâ‚a hâ‚‚a
  simp [toMeromorphicNFOn, hf, â† toMeromorphicNFAt_eq_self.1 hâ‚a.meromorphicNFAt]

/-- Conversion to normal form on `U` does not affect the divisor. -/
theorem divisor_toMeromorphicNFOn [CompleteSpace E] (hf : MeromorphicOn f U) :
    MeromorphicOn.divisor f U = MeromorphicOn.divisor (toMeromorphicNFOn f U) U := by
  rw [â† hf.divisor_congr_codiscreteWithin (toMeromorphicNFOn_eqOn_codiscrete hf)]
  exact toMeromorphicNFOn_eq_self_on_compl hf

/-- If `f` is meromorphic on `U` and `x âˆˆ U`, then `f` and its conversion to
normal form on `U` agree in a punctured neighborhood of `x`. -/
theorem MeromorphicOn.toMeromorphicNFOn_eq_self_on_nhdNE [CompleteSpace E]
    (hf : MeromorphicOn f U) (hx : x âˆˆ U) :
    f =á¶ [ğ“[â‰ ] x] toMeromorphicNFOn f U := by
  filter_upwards [(hf x hx).eventually_analyticAt] with a ha
  simp [toMeromorphicNFOn, hf, â† toMeromorphicNFAt_eq_self.1 ha.meromorphicNFAt]

/-- If `f` is meromorphic on `U` and `x âˆˆ U`, then conversion to normal form at
`x` and conversion to normal form on `U` agree in a neighborhood of `x`. -/
theorem toMeromorphicNFOn_eq_toMeromorphicNFAt_on_nhdNE [CompleteSpace E] (hf : MeromorphicOn f U)
    (hx : x âˆˆ U) :
    toMeromorphicNFOn f U =á¶ [ğ“ x] toMeromorphicNFAt f x := by
  apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE
  exact (hf.toMeromorphicNFOn_eq_self_on_nhdNE hx).symm.trans (hf x hx).eq_nhdNE_toMeromorphicNFAt
  simp [toMeromorphicNFOn, hf, hx]

variable (f U) in
/-- After conversion to normal form at `x`, the function has normal form. -/
theorem meromorphicNFOn_toMeromorphicNFOn [CompleteSpace E] :
    MeromorphicNFOn (toMeromorphicNFOn f U) U := by
  by_cases hf : MeromorphicOn f U
  Â· intro z hz
    rw [meromorphicNFAt_congr (toMeromorphicNFOn_eq_toMeromorphicNFAt_on_nhdNE hf hz)]
    exact meromorphicNFAt_toMeromorphicNFAt
  Â· simp [hf]
    apply AnalyticOnNhd.meromorphicNFOn
    exact analyticOnNhd_const

/-- If `f` has normal form on `U`, then `f` equals `toMeromorphicNFOn f U`. -/
@[simp] theorem toMeromorphicNFOn_eq_self [CompleteSpace E] :
    MeromorphicNFOn f U â†” f = toMeromorphicNFOn f U := by
  constructor <;> intro h
  Â· ext x
    by_cases hx : x âˆˆ U
    Â· simp only [toMeromorphicNFOn, h.meromorphicOn, â†“reduceDIte, hx]
      rw [â† toMeromorphicNFAt_eq_self.1 (h x hx)]
    Â· simp [toMeromorphicNFOn, h.meromorphicOn, hx]
  Â· rw [h]
    apply meromorphicNFOn_toMeromorphicNFOn


/- ######################################################## -/

theorem toMeromorphicNFOn_changeOrder [CompleteSpace ğ•œ]
  {f : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  {zâ‚€ : ğ•œ}
  (hf : MeromorphicOn f U)
  (hzâ‚€ : zâ‚€ âˆˆ U) :
  ((meromorphicNFOn_toMeromorphicNFOn f U) zâ‚€ hzâ‚€).meromorphicAt.order = (hf zâ‚€ hzâ‚€).order := by
  apply MeromorphicAt.order_congr
  exact (hf.toMeromorphicNFOn_eq_self_on_nhdNE hzâ‚€).symm


theorem MeromorphicOn.divisor_of_toMeromorphicNFOn [CompleteSpace ğ•œ]
  {f : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  (hf : MeromorphicOn f U) :
  divisor f U = divisor (toMeromorphicNFOn f U) U := by
  ext z
  by_cases hz : z âˆˆ U
  Â· simp [hf, (meromorphicNFOn_toMeromorphicNFOn f U).meromorphicOn, hz]
    congr 1
    apply MeromorphicAt.order_congr
    exact toMeromorphicNFOn_eq_self_on_nhdNE hf hz
  Â· simp [hz]
