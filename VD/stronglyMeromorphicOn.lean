import Mathlib.Algebra.BigOperators.Finprod
import Mathlib.Analysis.Meromorphic.Divisor.MeromorphicFunction
import VD.ToMathlib.MeromorphicNFAt
import VD.meromorphicAt

open Topology

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f : ğ•œ â†’ E}
  {x : ğ•œ}
  {U : Set ğ•œ}

/-!
# Normal form of meromorphic functions on a given set

## Definition
-/

/-- A function is 'meromorphic in normal form' on `U` if has normal form at
every point of `U`. -/
def MeromorphicNFOn (f : ğ•œ â†’ E) (U : Set ğ•œ) := âˆ€ z âˆˆ U, MeromorphicNFAt f z

/-!
## Relation to other properties of functions
-/

/-- If a function is meromorphic in normal form on `U`, then it is meromorphic on `U`. -/
theorem MeromorphicNFOn.meromorphicOn (hf : MeromorphicNFOn f U) :
    MeromorphicOn f U := fun z hz â†¦ (hf z hz).meromorphicAt

/-- If a function is meromorphic in normal form on `U`, then its divisor is
non-negative iff it is analytic. -/
theorem MeromorphicNFOn.nonneg_divisor_iff_analyticOnNhd [CompleteSpace E]
    (hâ‚f : MeromorphicNFOn f U) :
    0 â‰¤ MeromorphicOn.divisor f U â†” AnalyticOnNhd ğ•œ f U := by
  constructor <;> intro h
  Â· intro x hx
    rw [â† (hâ‚f x hx).order_nonneg_iff_analyticAt]
    have := h x
    simp only [DivisorOn.coe_zero, Pi.zero_apply, hâ‚f.meromorphicOn, hx,
      MeromorphicOn.divisor_apply, le_refl, implies_true, WithTop.le_untopD_iff,
      WithTop.coe_zero] at this
    assumption
  Â· intro x
    by_cases hx : x âˆˆ U
    Â· simp only [DivisorOn.coe_zero, Pi.zero_apply, hâ‚f.meromorphicOn, hx,
        MeromorphicOn.divisor_apply, le_refl, implies_true, WithTop.le_untopD_iff,
        WithTop.coe_zero]
      exact (hâ‚f x hx).order_nonneg_iff_analyticAt.2 (h x hx)
    Â· simp [hâ‚f.meromorphicOn, hx]

/- Analytic functions are meromorphic in normal form. -/
theorem AnalyticOnNhd.meromorphicNFOn (hâ‚f : AnalyticOnNhd ğ•œ f U) :
    MeromorphicNFOn f U := fun z hz â†¦ (hâ‚f z hz).meromorphicNFAt

/-!
## Level sets of the order function
-/

/-- Criterion to ensure that the order of a meromorphic function in normal form
is not infinity. See `MeromorphicOn.exists_order_ne_top_iff_forall` for a related
criterion for arbitrarymeromorphic functions. -/
theorem MeromorphicNFOn.order_ne_top_if_exists_value_ne_zero (hâ‚f : MeromorphicNFOn f U)
    (hâ‚‚f : âˆƒ u : U, f u â‰  0) (hU : IsConnected U) :
    âˆ€ u : U, (hâ‚f u u.2).meromorphicAt.order â‰  âŠ¤ := by
  rw [â† hâ‚f.meromorphicOn.exists_order_ne_top_iff_forall hU]
  obtain âŸ¨u, huâŸ© := hâ‚‚f
  use u
  rw [â† (hâ‚f u u.2).order_eq_zero_iff] at hu
  simp [hu]

/-!
## Divisors of meromorphic functions in normal form.
-/

theorem MeromorphicNFOn.zero_set_eq_divisor_support [CompleteSpace E] (hâ‚f : MeromorphicNFOn f U)
    (hâ‚‚f : âˆƒ u : U, f u â‰  0) (hU : IsConnected U) :
    U âˆ© fâ»Â¹' {0} = (Function.support (MeromorphicOn.divisor f U)) := by
  ext u
  constructor <;> intro hu
  Â· simp_all only [ne_eq, Subtype.exists, exists_prop, Set.mem_inter_iff, Set.mem_preimage,
      Set.mem_singleton_iff, Function.mem_support, hâ‚f.meromorphicOn, MeromorphicOn.divisor_apply,
      WithTop.untopD_eq_self_iff, WithTop.coe_zero, (hâ‚f u hu.1).order_eq_zero_iff,
      not_true_eq_false, false_or]
    apply hâ‚f.order_ne_top_if_exists_value_ne_zero _ hU âŸ¨u, hu.1âŸ©
    obtain âŸ¨a, haâŸ© := hâ‚‚f
    use âŸ¨a, ha.1âŸ©, ha.2
  Â· simp only [Function.mem_support, ne_eq] at hu
    constructor
    Â· exact (MeromorphicOn.divisor f U).supportWithinDomain hu
    Â· rw [Set.mem_preimage, Set.mem_singleton_iff]
      have := (hâ‚f u ((MeromorphicOn.divisor f U).supportWithinDomain hu)).order_eq_zero_iff.not
      simp only [hâ‚f.meromorphicOn, (MeromorphicOn.divisor f U).supportWithinDomain hu,
        MeromorphicOn.divisor_apply, WithTop.untopD_eq_self_iff, WithTop.coe_zero, not_or] at hu
      simp_all [this, hu.1]

theorem MeromorphicNFOn_of_mul_analytic'
  {f : ğ•œ â†’ E}
  {g : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  (hâ‚g : AnalyticOnNhd ğ•œ g U)
  (hâ‚‚g : âˆ€ u : U, g u â‰  0)
  (hâ‚f : MeromorphicNFOn f U) :
  MeromorphicNFOn (g â€¢ f) U := by
  intro z hz
  apply MeromorphicNFAt.meromorphicNFAt_of_smul_analytic (hâ‚f z hz) (hâ‚g z hz)
  exact hâ‚‚g âŸ¨z, hzâŸ©

/- Make strongly MeromorphicOn -/
noncomputable def makeMeromorphicNFOn
  (f : ğ•œ â†’ ğ•œ) (U : Set ğ•œ) :
  ğ•œ â†’ ğ•œ := by
  intro z
  by_cases hz : z âˆˆ U
  Â· exact toMeromorphicNFAt f z z
  Â· exact f z

theorem makeMeromorphicNFOn_changeDiscrete [CompleteSpace ğ•œ]
  {f : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  {zâ‚€ : ğ•œ}
  (hf : MeromorphicOn f U)
  (hzâ‚€ : zâ‚€ âˆˆ U) :
  makeMeromorphicNFOn f U =á¶ [ğ“[â‰ ] zâ‚€] f := by
  apply Filter.eventually_iff_exists_mem.2
  let A := (hf zâ‚€ hzâ‚€).eventually_analyticAt
  obtain âŸ¨V, hâ‚V, hâ‚‚VâŸ©  := Filter.eventually_iff_exists_mem.1 A
  use V
  constructor
  Â· assumption
  Â· intro v hv
    unfold makeMeromorphicNFOn
    by_cases hâ‚‚v : v âˆˆ U
    Â· simp [hâ‚‚v]
      let B := (hâ‚‚V v hv).meromorphicNFAt
      let Z := toMeromorphicNFAt_eq_self.1 B
      rw [eq_comm]
      rw [â† Z]
    Â· simp [hâ‚‚v]

theorem makeMeromorphicNFOn_changeDiscrete' [CompleteSpace ğ•œ]
  {f : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  {zâ‚€ : ğ•œ}
  (hf : MeromorphicOn f U)
  (hzâ‚€ : zâ‚€ âˆˆ U) :
  makeMeromorphicNFOn f U =á¶ [ğ“ zâ‚€] toMeromorphicNFAt f zâ‚€ := by
  apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE
  Â· apply Filter.EventuallyEq.trans (makeMeromorphicNFOn_changeDiscrete hf hzâ‚€)
    exact MeromorphicAt.eq_nhdNE_toMeromorphicNFAt (hf zâ‚€ hzâ‚€)
  Â· rw [makeMeromorphicNFOn]
    simp [hzâ‚€]

theorem makeMeromorphicNFOn_changeDiscrete'' [CompleteSpace ğ•œ]
  {f : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  (hf : MeromorphicOn f U) :
  f =á¶ [Filter.codiscreteWithin U] makeMeromorphicNFOn f U := by

  rw [Filter.eventuallyEq_iff_exists_mem]
  use { x | AnalyticAt ğ•œ f x }, hf.analyticAt_codiscreteWithin
  intro x hx
  simp at hx
  rw [makeMeromorphicNFOn]
  by_cases hâ‚x : x âˆˆ U
  Â· simp [hâ‚x]
    rw [â† toMeromorphicNFAt_eq_self.1 hx.meromorphicNFAt]
  Â· simp [hâ‚x]

theorem MeromorphicNFOn_of_makeMeromorphicNFOn [CompleteSpace ğ•œ]
  {f : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  (hf : MeromorphicOn f U) :
  MeromorphicNFOn (makeMeromorphicNFOn f U) U := by
  intro z hz
  let A := makeMeromorphicNFOn_changeDiscrete' hf hz
  rw [meromorphicNFAt_congr A]
  exact meromorphicNFAt_toMeromorphicNFAt

theorem makeMeromorphicNFOn_changeOrder [CompleteSpace ğ•œ]
  {f : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  {zâ‚€ : ğ•œ}
  (hf : MeromorphicOn f U)
  (hzâ‚€ : zâ‚€ âˆˆ U) :
  (MeromorphicNFOn_of_makeMeromorphicNFOn hf zâ‚€ hzâ‚€).meromorphicAt.order = (hf zâ‚€ hzâ‚€).order := by
  apply MeromorphicAt.order_congr
  exact makeMeromorphicNFOn_changeDiscrete hf hzâ‚€

theorem MeromorphicOn.divisor_of_makeMeromorphicNFOn [CompleteSpace ğ•œ]
  {f : ğ•œ â†’ ğ•œ}
  {U : Set ğ•œ}
  (hf : MeromorphicOn f U) :
  divisor f U = divisor (makeMeromorphicNFOn f U) U := by
  ext z
  by_cases hz : z âˆˆ U
  Â· simp [hf, (MeromorphicNFOn_of_makeMeromorphicNFOn hf).meromorphicOn, hz]
    congr 1
    apply MeromorphicAt.order_congr
    exact Filter.EventuallyEq.symm (makeMeromorphicNFOn_changeDiscrete hf hz)
  Â· simp [hz]
