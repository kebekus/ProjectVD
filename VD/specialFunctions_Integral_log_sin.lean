import VD.ToMathlib.meromorphicOn_integrability

open Filter Interval Real

-- 150 lines max

theorem analyticOnNhd_cos :
    AnalyticOnNhd â„ Real.cos Set.univ := by
  apply analyticOnNhd_realPart (f := Complex.cos)
  apply Complex.analyticOnNhd_univ_iff_differentiable.mpr
  exact Complex.differentiable_cos

/--
The set where an analytic function has zero or infinite order is discrete within
its domain of analyticity.
-/
theorem AnalyticOnNhd.codiscreteWithin_setOf_analyticOrderAt_eq_zero_or_top {f : â„ â†’ â„} {U : Set â„}
    (hf : AnalyticOnNhd â„ f U) :
    {u : â„ | analyticOrderAt f u = 0 âˆ¨ analyticOrderAt f u = âŠ¤} âˆˆ Filter.codiscreteWithin U := by
  simp_rw [mem_codiscreteWithin, Filter.disjoint_principal_right]
  intro x hx
  rcases (hf x hx).eventually_eq_zero_or_eventually_ne_zero with hâ‚f | hâ‚f
  Â· filter_upwards [eventually_nhdsWithin_of_eventually_nhds hâ‚f.eventually_nhds] with a ha
    simp [analyticOrderAt_eq_top, ha]
  Â· filter_upwards [hâ‚f] with a ha
    simp +contextual [(hf a _).analyticOrderAt_eq_zero, ha]

/--
If `f` is analytic on `ğ•œ` and non-zero at one point, then the set of non-zeros is codiscrete.
-/
lemma AnalyticOnNhd.preimg_zero_comp_mem_codiscrete {x : â„} {f : â„ â†’ â„}
    (hf : AnalyticOnNhd â„ f Set.univ) (hâ‚‚f : f x â‰  0) :
    f â»Â¹' {0}á¶œ âˆˆ codiscrete â„ := by
  filter_upwards [hf.codiscreteWithin_setOf_analyticOrderAt_eq_zero_or_top] with a
  rw [â† (hf x trivial).analyticOrderAt_eq_zero] at hâ‚‚f
  have {u : â„} : analyticOrderAt f u â‰  âŠ¤ := by
    apply (hf.exists_analyticOrderAt_ne_top_iff_forall (by exact isConnected_univ)).1 _ âŸ¨u, trivialâŸ©
    use âŸ¨x, trivialâŸ©
    simp_all
  simp only [Set.mem_univ, (hf a _).analyticOrderAt_eq_zero, ne_eq, Set.preimage_compl,
    Set.mem_compl_iff, Set.mem_preimage, Set.mem_singleton_iff]
  tauto

/--
Helper lemma for `integral_log_sin_zero_pi_div_two`: The integral of `log âˆ˜ sin`
on `0 â€¦ Ï€` is double the integral on `0 â€¦ Ï€/2`.
-/
lemma integral_log_sin_zero_pi_eq_two_mul_integral_log_sin_zero_pi_div_two :
    âˆ« x in (0)..Ï€, log (sin x) = 2 * âˆ« x in (0)..(Ï€ / 2), log (sin x) := by
  rw [â† intervalIntegral.integral_add_adjacent_intervals (a := 0) (b := Ï€ / 2) (c := Ï€)
    (by apply intervalIntegrable_log_sin) (by apply intervalIntegrable_log_sin)]
  conv =>
    left; right; arg 1
    intro x
    rw [â† sin_pi_sub]
  rw [intervalIntegral.integral_comp_sub_left (fun x â†¦ log (sin x)), sub_self,
    (by linarith : Ï€ - Ï€ / 2 = Ï€ / 2)]
  ring!

/--
The integral of `log âˆ˜ sin` on `0 â€¦ Ï€/2` equals `-log 2 * Ï€ / 2`.
-/
theorem integral_log_sin_zero_pi_div_two : âˆ« x in (0)..(Ï€ / 2), log (sin x) = -log 2 * Ï€ / 2 := by
  calc âˆ« x in (0)..(Ï€ / 2), log (sin x)
    _ = âˆ« x in (0)..(Ï€ / 2), (log (sin (2 * x)) - log 2 - log (cos x)) := by
      apply intervalIntegral.integral_congr_codiscreteWithin
      apply Filter.codiscreteWithin.mono (by tauto : Î™ 0 (Ï€ / 2) âŠ† Set.univ)
      have tâ‚€ : sin â»Â¹' {0}á¶œ âˆˆ Filter.codiscrete â„ := by
        apply analyticOnNhd_sin.preimg_zero_comp_mem_codiscrete (x := Ï€ / 2)
        simp
      have tâ‚ : cos â»Â¹' {0}á¶œ âˆˆ Filter.codiscrete â„ := by
        apply analyticOnNhd_cos.preimg_zero_comp_mem_codiscrete (x := 0)
        simp
      filter_upwards [tâ‚€, tâ‚] with y hâ‚y hâ‚‚y
      simp_all only [Set.preimage_compl, Set.mem_compl_iff, Set.mem_preimage, Set.mem_singleton_iff,
        sin_two_mul, ne_eq, mul_eq_zero, OfNat.ofNat_ne_zero, or_self, not_false_eq_true, log_mul]
      ring
    _ = (âˆ« x in (0)..(Ï€ / 2), log (sin (2 * x))) - Ï€ / 2 * log 2 - âˆ« x in (0)..(Ï€ / 2), log (cos x) := by
      rw [intervalIntegral.integral_sub _ _,
        intervalIntegral.integral_sub _ intervalIntegrable_const,
        intervalIntegral.integral_const]
      simp
      Â· simpa using (intervalIntegrable_log_sin (a := 0) (b := Ï€)).comp_mul_left 2
      Â· apply IntervalIntegrable.sub _ intervalIntegrable_const
        simpa using (intervalIntegrable_log_sin (a := 0) (b := Ï€)).comp_mul_left 2
      Â· exact intervalIntegrable_log_cos
    _ = (âˆ« x in (0)..(Ï€ / 2), log (sin (2 * x))) - Ï€ / 2 * log 2 - âˆ« x in (0)..(Ï€ / 2), log (sin x) := by
      simp [â† sin_pi_div_two_sub, intervalIntegral.integral_comp_sub_left (fun x â†¦ log (sin x)) (Ï€ / 2)]
    _ = -log 2 * Ï€ / 2 := by
      simp only [intervalIntegral.integral_comp_mul_left (f := fun x â†¦ log (sin x)) two_ne_zero,
        mul_zero, (by linarith : 2 * (Ï€ / 2) = Ï€), integral_log_sin_zero_pi_eq_two_mul_integral_log_sin_zero_pi_div_two, smul_eq_mul, ne_eq,
        OfNat.ofNat_ne_zero, not_false_eq_true, inv_mul_cancel_leftâ‚€, sub_sub_cancel_left, neg_mul]
      linarith

/--
The integral of `log âˆ˜ sin` on `0 â€¦ Ï€` equals `-log 2 * Ï€`.
-/
theorem integral_log_sin_zero_pi : âˆ« x in (0)..Ï€, log (sin x) = -log 2 * Ï€ := by
  rw [integral_log_sin_zero_pi_eq_two_mul_integral_log_sin_zero_pi_div_two, integral_log_sin_zero_pi_div_two]
  ring
