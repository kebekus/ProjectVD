/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Divisor
import Mathlib.Analysis.Meromorphic.NormalForm

/-!
# Factorized Rational Functions

This file discusses functions `ğ•œ â†’ ğ•œ` of the form `âˆá¶  u, (Â· - u) ^ d u`, where
`d : ğ•œ â†’ â„¤` is integer-valued. We show that these "factorized rational
functions" are meromorphic in normal form, with divisor equal to `d`.

TODO: Show that every meromorphic functions on a compact set is equivalent,
modulo equality on codiscrete sets, the the product of a factorized rational
function and an analytic function without zeros.
-/

open Function

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]
  {U : Set ğ•œ}
  {z : ğ•œ}

/--
Helper Lemma: Identify the support of `d` as the mulsupport of the product
defining the factorized rational function.
-/
lemma Function.FactorizedRational.mulSupport (d : ğ•œ â†’ â„¤) :
    (Function.mulSupport fun u â†¦ (Â· - u) ^ d u) = d.support := by
  ext u
  constructor
  Â· intro h
    simp_all only [Function.mem_mulSupport, ne_eq, Function.mem_support]
    by_contra hCon
    simp only [hCon, zpow_zero] at h
    tauto
  Â· intro h
    simp only [Function.mem_mulSupport, ne_eq]
    by_contra hCon
    have := congrFun hCon u
    simp only [Pi.pow_apply, sub_self, Pi.ofNat_apply] at this
    have : (0 : ğ•œ) ^ d u â‰  0 := ne_zero_of_eq_one this
    rw [zpow_ne_zero_iff h] at this
    tauto

lemma Function.FactorizedRational.analyticAt {d : ğ•œ â†’ â„¤} {x : ğ•œ} (h : 0 â‰¤ d x) :
    AnalyticAt ğ•œ (âˆá¶  u, (Â· - u) ^ d u) x := by
  by_cases hâ‚ : Set.Finite d.support
  Â· rw [â† Function.FactorizedRational.mulSupport d] at hâ‚
    rw [finprod_eq_prod _ hâ‚]
    have : (âˆ i âˆˆ hâ‚.toFinset, (fun x â†¦ x - i) ^ d i) = (fun x â†¦ âˆ i âˆˆ hâ‚.toFinset, (x - i) ^ d i) := by
      ext x
      simp
    rw [this]
    apply Finset.analyticAt_prod
    intro u hu
    by_cases hâ‚‚ : x = u
    Â· apply AnalyticAt.fun_zpow_nonneg
      fun_prop
      rwa [â† hâ‚‚]
    Â· apply AnalyticAt.fun_zpow
      fun_prop
      rwa [sub_ne_zero]
  Â· rw [â† Function.FactorizedRational.mulSupport d] at hâ‚
    rw [finprod_of_infinite_mulSupport hâ‚]
    apply analyticAt_const

lemma Function.FactorizedRational.zeroAt {d : ğ•œ â†’ â„¤} {x : ğ•œ} (h : d x = 0) :
    (âˆá¶  u, (Â· - u) ^ d u) x â‰  0 := by
  by_cases hâ‚ : Set.Finite d.support
  Â· rw [â† Function.FactorizedRational.mulSupport d] at hâ‚
    rw [finprod_eq_prod _ hâ‚]
    rw [Finset.prod_apply]
    rw [Finset.prod_ne_zero_iff]
    intro z hz
    simp only [Pi.pow_apply, ne_eq]
    by_cases hâ‚‚ : z = x
    Â· simp_all
    Â· apply zpow_ne_zero
      rw [sub_ne_zero]
      tauto
  Â· rw [â† Function.FactorizedRational.mulSupport d] at hâ‚
    rw [finprod_of_infinite_mulSupport hâ‚]
    simp

open Classical in
lemma Function.FactorizedRational.analyticAt' {d : ğ•œ â†’ â„¤} (uâ‚€ : ğ•œ) (hd : d.support.Finite) :
    (âˆá¶  u, (Â· - u) ^ d u) = ((Â· - uâ‚€) ^ d uâ‚€) * (âˆá¶  u, (Â· - u) ^ (update d uâ‚€ 0 u)) := by
  by_cases hâ‚d : d uâ‚€ = 0
  Â· rw [â† eq_update_self_iff.2 hâ‚d]
    simp [hâ‚d]
  have tâ‚€ : (mulSupport fun u â†¦ (fun x â†¦ x - u) ^ d u) âŠ† hd.toFinset := by
    simp [Function.FactorizedRational.mulSupport]
  rw [finprod_eq_prod_of_mulSupport_subset _ tâ‚€]
  have tâ‚ : uâ‚€ âˆˆ hd.toFinset := by
    simp_all
  rw [â† Finset.mul_prod_erase hd.toFinset _ tâ‚]
  congr 1
  have tâ‚‚ : (mulSupport fun u â†¦ (fun x â†¦ x - u) ^ (update d uâ‚€ 0 u)) âŠ† hd.toFinset.erase uâ‚€ := by
    rw [Function.FactorizedRational.mulSupport]
    intro x hx
    by_cases hâ‚x : x = uâ‚€ <;> simp_all
  rw [finprod_eq_prod_of_mulSupport_subset _ tâ‚‚]
  apply Finset.prod_congr rfl
  intro x hx
  rw [eq_comm]
  simp_all

/-- Laurent polynomials are meromorphic in normal form on `âŠ¤`. -/
theorem meromorphicNFOn_laurentPolynomial_top (d : ğ•œ â†’ â„¤) :
    MeromorphicNFOn (âˆá¶  u, (Â· - u) ^ d u) âŠ¤ := by
  classical
  by_cases hd : d.support.Finite
  Â· intro z hz
    rw [Function.FactorizedRational.analyticAt' z hd]
    right
    use d z, (âˆá¶  u, (Â· - u) ^ update d z 0 u)
    constructor
    Â· simp [Function.FactorizedRational.analyticAt]
    Â· constructor
      Â· apply FactorizedRational.zeroAt
        simp
      Â· simp
  Â· rw [â† Function.FactorizedRational.mulSupport d] at hd
    rw [finprod_of_infinite_mulSupport hd]
    apply AnalyticOnNhd.meromorphicNFOn
    apply analyticOnNhd_const


/-- Laurent polynomials are meromorphic in normal form on arbitrary subsets of `ğ•œ`. -/
theorem meromorphicNFOn_laurentPolynomial (d : ğ•œ â†’ â„¤) (U : Set ğ•œ) :
    MeromorphicNFOn (âˆá¶  u, (Â· - u) ^ d u) U := by
  intro z hz
  exact meromorphicNFOn_laurentPolynomial_top d (trivial)

/-- The order of the Laurent polynomial `(âˆá¶  u, fun z â†¦ (z - u) ^ d u)` at z equals `d z`. -/
theorem order_laurentPolynomial {z : ğ•œ} (d : ğ•œ â†’ â„¤) (hâ‚d : Set.Finite d.support) :
    (((meromorphicNFOn_laurentPolynomial_top d).meromorphicOn) z trivial).order = d z := by
  classical
  rw [MeromorphicAt.order_eq_int_iff]
  use (âˆá¶  u, (Â· - u) ^ update d z 0 u)
  constructor
  Â· simp [Function.FactorizedRational.analyticAt]
  Â· constructor
    Â· apply FactorizedRational.zeroAt
      simp
    Â· filter_upwards
      rw [Function.FactorizedRational.analyticAt' z hâ‚d]
      intro a
      simp

/--
Laurent polynomials are nowhere locally constant zero.
-/
theorem order_LaurentPolynomial_ne_top {z : ğ•œ} (d : ğ•œ â†’ â„¤) :
    (meromorphicNFOn_laurentPolynomial_top d (trivial : z âˆˆ âŠ¤)).meromorphicAt.order â‰  âŠ¤ := by
  by_cases hd : Set.Finite (Function.support d)
  Â· simp [order_laurentPolynomial d hd]
  Â· rw [â† Function.FactorizedRational.mulSupport] at hd
    have : AnalyticAt ğ•œ (1 : ğ•œ â†’ ğ•œ) z := analyticAt_const
    simp [finprod_of_infinite_mulSupport hd, this.meromorphicAt_order,
      this.order_eq_zero_iff.2 (by simp)]

/--
If `D` is a divisor, then the function associated with the divisor of the Laurent polynomial equals
`D`.
-/
theorem divisor_laurentPolynomial_within [CompleteSpace ğ•œ] {U : Set ğ•œ}
    (D : Function.locallyFinsuppWithin U â„¤) (hD : Set.Finite D.support) :
    MeromorphicOn.divisor (âˆá¶  u, (Â· - u) ^ D u) U = D := by
  ext z
  by_cases hz : z âˆˆ U
  Â· simp [(meromorphicNFOn_laurentPolynomial D U).meromorphicOn, hz,
      MeromorphicOn.divisor_apply, order_laurentPolynomial D hD]
  Â· simp [hz]
