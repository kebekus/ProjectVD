/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Divisor
import Mathlib.Analysis.SpecialFunctions.Log.Basic
import VD.ToMathlib.Divisor_add

open MeromorphicOn Metric Real

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {U : Set ğ•œ}
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]

/-!
# The Counting Function of a Meromorphic Function

For nontrivially normed fields `ğ•œ`, this file defines the logarithmic counting
function of a meromorphic function defined on `ğ•œ`, as used in Value
Distribution Theory.

The counting function of a meromorphic function `f` is a logarithmically
weighted measure of the number of times the function `f` takes a given value `a`
within the disk `âˆ£zâˆ£ â‰¤ r`, counting multiplicities.  See Section~VI.1 of [Lang:
Introduction to Complex Hyperbolic
Spaces](https://link.springer.com/book/10.1007/978-1-4757-1945-1) or Section~1.1
of [Noguchi-Winkelmann: Nevanlinna Theory in Several Complex Variables and
Diophantine
Approximation](https://link.springer.com/book/10.1007/978-4-431-54571-2) for a
detailed discussion.

We define the logarithmic counting function first for function with locally
finite support on `ğ•œ` and then specialize to the setting where the function
with locally finite support is the pole or zero-divisor of a meromorphic
function.
-/

/-!
## Supporting Notation
-/

namespace Function.locallyFinsuppWithin

/--
Shorthand notation for the restriction of a function with locally finite support
within âŠ¤ to the closed unit ball of radius `r`.
-/
noncomputable def toBall (r : â„) :
    locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤ â†’+ locallyFinsuppWithin (closedBall (0 : ğ•œ) |r|) â„¤ := by
  apply restrictMonoidHom
  tauto

/--
Restriction commutes with subtraction.
-/
lemma toBall_sub
    {Dâ‚ Dâ‚‚ : locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤} {r : â„} :
    (Dâ‚ - Dâ‚‚).toBall r = Dâ‚.toBall r - Dâ‚‚.toBall r := by
  simp

/-!
## The Logarithmic Counting Function of a Function with Locally Finite Support
-/

/--
Definition of the logarithmic counting function for a function with locally
finite support within `âŠ¤`.
-/
noncomputable def logCounting [ProperSpace ğ•œ] :
    locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤ â†’+ (â„ â†’ â„) where
  toFun D := fun r â†¦ âˆ‘á¶  z, D.toBall r z * log (r * â€–zâ€–â»Â¹) + (D 0) * log r
  map_zero' := by
    simp
    rfl
  map_add' Dâ‚ Dâ‚‚ := by
    simp only [Set.top_eq_univ, map_add, coe_add, Pi.add_apply, Int.cast_add]
    ext r
    have {A B C D : â„} : A + B + (C + D) = A + C + (B + D) := by ring
    rw [Pi.add_apply, this]
    congr 1
    Â· have hâ‚s : ((Dâ‚.toBall r).support âˆª (Dâ‚‚.toBall r).support).Finite := by
        apply Set.finite_union.2
        constructor
        <;> apply finiteSupport _ (isCompact_closedBall 0 |r|)
      repeat
        rw [finsum_eq_sum_of_support_subset (s := hâ‚s.toFinset)]
      simp_rw [â† Finset.sum_add_distrib, â† add_mul]
      --
      intro x hx
      simp_all
      --
      intro x hx
      simp_all
      --
      intro x hx
      by_contra hCon
      simp_all
    Â· ring

/-- The value of the counting function at zero is zero. -/
@[simp] lemma logCounting_eval_zero [ProperSpace ğ•œ]
    (D : locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤) :
    logCounting D 0 = 0 := by
  rw [logCounting]
  simp

end Function.locallyFinsuppWithin

/-!
## The Logarithmic Counting Function of a Meromorphic Function
-/

namespace VD

variable [ProperSpace ğ•œ]

/-- The logarithmic counting function of a meromorphic function. -/
noncomputable def logCounting (f : ğ•œ â†’ E) (a : WithTop E) :
    â„ â†’ â„ := by
  by_cases h : a = âŠ¤
  Â· exact (divisor f âŠ¤)â».logCounting
  Â· exact (divisor (fun z â†¦ f z - a.untopâ‚€) âŠ¤)âº.logCounting

lemma logCounting_finite {f : ğ•œ â†’ E} {a : E} :
    logCounting f a = (divisor (fun z â†¦ f z - a) âŠ¤)âº.logCounting := by
  simp [logCounting]

lemma logCounting_finite_eq_logCounting_zero_of_shifted {f : ğ•œ â†’ E} {a : E} :
    logCounting f a = logCounting (f - fun _ â†¦ a) 0 := by
  simp [logCounting]

lemma logCounting_zero {f : ğ•œ â†’ E} :
    logCounting f 0 = (divisor f âŠ¤)âº.logCounting := by
  simp [logCounting]

lemma logCounting_top {f : ğ•œ â†’ E} :
    logCounting f âŠ¤ = (divisor f âŠ¤)â».logCounting := by
  simp [logCounting]

lemma logCounting_eval_zero {f : ğ•œ â†’ E} {a : WithTop E}:
    logCounting f a 0 = 0 := by
  by_cases h : a = âŠ¤ <;> simp [h, logCounting]

theorem log_counting_zero_sub_logCounting_top {f : ğ•œ â†’ E} :
    (divisor f âŠ¤).logCounting = logCounting f 0 - logCounting f âŠ¤ := by
  simp [logCounting_zero, logCounting_top]
  nth_rw 1 [â† posPart_sub_negPart (divisor f Set.univ)]
  conv =>
    left
    rw [Function.locallyFinsuppWithin.logCounting.map_sub (divisor f Set.univ)âº (divisor f Set.univ)â»]

  simp
  simp_rw [â† Function.locallyFinsuppWithin.logCounting.map_sub]

/-!
## Elementary Properties of the Counting Function
-/

theorem logCounting_inv [CompleteSpace ğ•œ] {f : ğ•œ â†’ ğ•œ} :
    logCounting f 0 = logCounting fâ»Â¹ âŠ¤ := by
  simp [logCounting_zero, logCounting_top]

theorem logCounting_add_analytic {f g : ğ•œ â†’ E} (hf : MeromorphicOn f âŠ¤)
    (hg : AnalyticOn ğ•œ g âŠ¤) :
    logCounting (f + g) âŠ¤ = logCounting f âŠ¤ := by
  simp only [logCounting, â†“reduceDIte,
    hf.divisor_add_analytic (isOpen_univ.analyticOn_iff_analyticOnNhd.1 hg)]

theorem logCounting_add_const {f : ğ•œ â†’ E} {a : E} (hf : MeromorphicOn f âŠ¤) :
    logCounting (f + fun _ â†¦ a) âŠ¤ = logCounting f âŠ¤ := by
  apply logCounting_add_analytic hf analyticOn_const

theorem logCounting_sub_const {f : ğ•œ â†’ E} {a : E} (hf : MeromorphicOn f âŠ¤) :
    logCounting (f - fun _ â†¦ a) âŠ¤ = logCounting f âŠ¤ := by
  have : f - (fun x â†¦ a) = f + fun x â†¦ -a := by
    funext x
    simp [sub_eq_add_neg]
  rw [this]
  apply logCounting_add_analytic hf analyticOn_const

end VD
