/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Divisor
import Mathlib.Analysis.SpecialFunctions.Log.Basic
import VD.ToMathlib.Divisor_add

open MeromorphicOn Metric Real

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {U : Set ğ•œ}
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]


/-!
# The Counting Function of a Meromorphic Function

For nontrivially normed fields `ğ•œ`, this file defines the logarithmic counting
function of a meromorphic function defined on `ğ•œ`, as used in Value
Distribution Theory.
-/

/-!
## Supporting Notation
-/

namespace Function.locallyFinsuppWithin

/--
Shorthand notation for the restriction of a function with locally finite support
within âŠ¤ to the closed unit ball of radius `r`.
-/
noncomputable def toBall
    (D : Function.locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤) (r : â„) :
    Function.locallyFinsuppWithin (closedBall (0 : ğ•œ) |r|) â„¤ :=
  D.restrict (by tauto : closedBall (0 : ğ•œ) |r| âŠ† âŠ¤)

/--
Restriction commutes with subtraction.
-/
lemma toBall_sub
    {Dâ‚ Dâ‚‚ : Function.locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤} {r : â„} :
    (Dâ‚ - Dâ‚‚).toBall r = Dâ‚.toBall r - Dâ‚‚.toBall r := by
  ext x
  by_cases hâ‚ : â€–xâ€– â‰¤ |r| <;> simp [toBall, restrict_apply, hâ‚]

end Function.locallyFinsuppWithin

/-!
## The Logarithmic Counting Function of a Function with Locally Finite Support
-/

/--
Definition of the logarithmic counting function for a function with locally
finite support within `âŠ¤`.
-/
noncomputable def Function.locallyFinsuppWithin.logCounting
    (D : Function.locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤) :
    â„ â†’ â„ :=
  fun r â†¦ âˆ‘á¶  z, D.toBall r z * log (r * â€–zâ€–â»Â¹) + (D 0) * log r

/-- The value of the counting function at zero is zero. -/
@[simp] lemma Function.locallyFinsuppWithin.logCounting_eval_zero
    (D : Function.locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤) :
    logCounting D 0 = 0 := by
  rw [logCounting]
  simp

/--
The logarithmic counting function commutes with subtraction.
-/
@[simp] lemma Function.locallyFinsuppWithin.logCounting_sub [ProperSpace ğ•œ]
    (Dâ‚ Dâ‚‚ : Function.locallyFinsuppWithin (âŠ¤ : Set ğ•œ) â„¤) :
    logCounting (Dâ‚ - Dâ‚‚) = logCounting Dâ‚ - logCounting Dâ‚‚ := by
  ext r
  simp [logCounting]
  have hâ‚s : ((Dâ‚.toBall r).support âˆª (Dâ‚‚.toBall r).support).Finite := by
    apply Set.finite_union.2
    constructor
    <;> apply Function.locallyFinsuppWithin.finiteSupport _ (isCompact_closedBall 0 |r|)
  have tâ‚ : (fun z â†¦ Dâ‚.toBall r z * log (r * â€–zâ€–â»Â¹)).support âŠ† hâ‚s.toFinset := by
    intro x hx
    simp_all
  rw [finsum_eq_sum_of_support_subset _ tâ‚]
  have tâ‚‚ : (fun z â†¦ Dâ‚‚.toBall r z * log (r * â€–zâ€–â»Â¹)).support âŠ† hâ‚s.toFinset := by
    intro x hx
    simp_all
  rw [finsum_eq_sum_of_support_subset _ tâ‚‚]
  have tâ‚â‚‚ : (fun z â†¦ (Dâ‚ - Dâ‚‚).toBall r z * log (r * â€–zâ€–â»Â¹)).support âŠ† hâ‚s.toFinset := by
    intro x hx
    by_contra hCon
    rw [Function.locallyFinsuppWithin.toBall_sub] at hx
    simp_all
  simp_rw [finsum_eq_sum_of_support_subset _ tâ‚â‚‚]
  have {A B C D : â„} : A + B - (C + D) = A - C + (B - D) := by
    ring
  simp_rw [this, â† Finset.sum_sub_distrib, â† sub_mul, Function.locallyFinsuppWithin.toBall_sub]
  simp

/-!
## The Logarithmic Counting Function of a Meromorphic Function
-/

namespace VD

/-- The logarithmic counting function of a meromorphic function. -/
noncomputable def logCounting (f : ğ•œ â†’ E) (a : WithTop E) :
    â„ â†’ â„ := by
  by_cases h : a = âŠ¤
  Â· exact (divisor f âŠ¤)â».logCounting
  Â· exact (divisor (fun z â†¦ f z - a.untopâ‚€) âŠ¤)âº.logCounting

lemma logCounting_finite {f : ğ•œ â†’ E} {a : E} :
    logCounting f a = (divisor (fun z â†¦ f z - a) âŠ¤)âº.logCounting := by
  simp [logCounting]

lemma logCounting_finite_eq_logCounting_zero_of_shifted {f : ğ•œ â†’ E} {a : E} :
    logCounting f a = logCounting (f - fun _ â†¦ a) 0 := by
  simp [logCounting]

lemma logCounting_zero {f : ğ•œ â†’ E} :
    logCounting f 0 = (divisor f âŠ¤)âº.logCounting := by
  simp [logCounting]

lemma logCounting_top {f : ğ•œ â†’ E} :
    logCounting f âŠ¤ = (divisor f âŠ¤)â».logCounting := by
  simp [logCounting]

lemma logCounting_eval_zero {f : ğ•œ â†’ E} {a : WithTop E}:
    logCounting f a 0 = 0 := by
  by_cases h : a = âŠ¤ <;> simp [h, logCounting]

theorem log_counting_zero_sub_logCounting_top [ProperSpace ğ•œ] {f : ğ•œ â†’ E} :
    (divisor f âŠ¤).logCounting = logCounting f 0 - logCounting f âŠ¤ := by
  simp [logCounting_zero, logCounting_top, â† Function.locallyFinsuppWithin.logCounting_sub]

/-!
## Elementary Properties of the Counting Function
-/

theorem logCounting_inv [CompleteSpace ğ•œ] {f : ğ•œ â†’ ğ•œ} :
    logCounting f 0 = logCounting fâ»Â¹ âŠ¤ := by
  simp [logCounting_zero, logCounting_top]

theorem logCounting_add_analytic {f g : ğ•œ â†’ E} (hf : MeromorphicOn f âŠ¤)
    (hg : AnalyticOn ğ•œ g âŠ¤) :
    logCounting (f + g) âŠ¤ = logCounting f âŠ¤ := by
  simp only [logCounting, â†“reduceDIte,
    hf.divisor_add_analytic (isOpen_univ.analyticOn_iff_analyticOnNhd.1 hg)]

theorem logCounting_add_const {f : ğ•œ â†’ E} {a : E} (hf : MeromorphicOn f âŠ¤) :
    logCounting (f + fun _ â†¦ a) âŠ¤ = logCounting f âŠ¤ := by
  apply logCounting_add_analytic hf analyticOn_const

theorem logCounting_sub_const {f : ğ•œ â†’ E} {a : E} (hf : MeromorphicOn f âŠ¤) :
    logCounting (f - fun _ â†¦ a) âŠ¤ = logCounting f âŠ¤ := by
  have : f - (fun x â†¦ a) = f + fun x â†¦ -a := by
    funext x
    simp [sub_eq_add_neg]
  rw [this]
  apply logCounting_add_analytic hf analyticOn_const

end VD
