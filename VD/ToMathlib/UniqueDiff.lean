import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Analysis.Calculus.FDeriv.RestrictScalars

variable
  {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
  {x : E} {s : Set E}

/--
Filter version of the statement that preimages of cobounded sets under the
algebra map are cobounded.
-/
theorem algebraMap_cobounded_le_cobounded :
    Filter.map (algebraMap ğ•œ ğ•œ') (Bornology.cobounded ğ•œ) â‰¤ Bornology.cobounded ğ•œ' := by
  intro c hc
  rw [Filter.mem_map, â† Bornology.isCobounded_def, â† Bornology.isBounded_compl_iff,
    isBounded_iff_forall_norm_le]
  obtain âŸ¨s, hsâŸ© := isBounded_iff_forall_norm_le.1
    (Bornology.isBounded_compl_iff.2 (Bornology.isCobounded_def.1 hc))
  use s
  exact fun x hx â†¦ by simpa [norm_algebraMap] using hs ((algebraMap ğ•œ ğ•œ') x) hx

/--
Given `x âˆˆ s` and a field extension `ğ•œ âŠ† ğ•œ'`, the tangent of `s` at `x` with
respect to `ğ•œ` is contained in the tangent of `s` at `x` with respect to `ğ•œ'`.
-/
theorem tangentConeAt_mono_field : tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ' s x := by
  intro Î± hÎ±
  simp [tangentConeAt] at hÎ± âŠ¢
  obtain âŸ¨c, d, âŸ¨a, hâ‚aâŸ©, hâ‚, hâ‚‚âŸ© := hÎ±
  use ((algebraMap ğ•œ ğ•œ') âˆ˜ c), d
  constructor
  Â· use a
  Â· constructor
    Â· intro Î² hÎ²
      rw [Filter.mem_map, Filter.mem_atTop_sets]
      obtain âŸ¨n, hnâŸ© := Filter.mem_atTop_sets.1 (Filter.mem_map.1 (hâ‚ (algebraMap_cobounded_le_cobounded (ğ•œ' := ğ•œ') hÎ²)))
      use n, fun _ _ â†¦ by simp_all
    Â· simpa

/--
Assume that `E` is a normed vector space over normed fields `ğ•œ âŠ† ğ•œ'` and that
`x âˆˆ s` is a point of unique differentiability with respect to the set `s` and
the smaller field `ğ•œ`, then `x` is also a point of unique differentiability
with respect to the set `s` and the larger field `ğ•œ`.
-/
theorem UniqueDiffWithinAt.mono_field (hâ‚‚s : UniqueDiffWithinAt ğ•œ s x) :
    UniqueDiffWithinAt ğ•œ' s x := by
  rw [uniqueDiffWithinAt_iff] at *
  simp_all only [and_true]
  apply Dense.mono _ hâ‚‚s.1
  trans â†‘(Submodule.span ğ•œ (tangentConeAt ğ•œ' s x))
  <;> simp [Submodule.span_mono tangentConeAt_mono_field]

/--
Assume that `E` is a normed vector space over normed fields `ğ•œ âŠ† ğ•œ'` and all
points of `s` are points of unique differentiability with respect to the smaller
field `ğ•œ`, then they are also points of unique differentiability with respect
to the larger field `ğ•œ`.
-/
theorem UniqueDiffOn.mono_field (hâ‚‚s : UniqueDiffOn ğ•œ s) :
    UniqueDiffOn ğ•œ' s := fun x hx â†¦ (hâ‚‚s x hx).mono_field
