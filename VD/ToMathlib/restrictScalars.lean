/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Calculus.FDeriv.RestrictScalars
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs

/-!
### Restricting Scalars in Iterated Derivatives

This file establishes standard theorems on restriction of scalars for iterated
FrÃ©chet derivatives, similar to those found in
`Mathlib.Analysis.Calculus.FDeriv.RestrictScalars`.
-/

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedSpace ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F]
  {x : E}

open Topology

theorem fderiv_restrictScalarsLinear_comp {n : â„•}
    {f : E â†’ (ContinuousMultilinearMap ğ•œ' (fun _ : Fin n â†¦ E) F)}
    (h : DifferentiableAt ğ•œ' f x) :
    (fderiv ğ•œ ((ContinuousMultilinearMap.restrictScalarsLinear ğ•œ) âˆ˜ f) x)
      = (ContinuousMultilinearMap.restrictScalars ğ•œ) âˆ˜ ((fderiv ğ•œ' f x).restrictScalars ğ•œ) := by
  rw [fderiv_comp _ (by fun_prop) (h.restrictScalars ğ•œ), ContinuousLinearMap.fderiv]
  ext a b
  simp [h.fderiv_restrictScalars ğ•œ]

theorem ContDiffAt.differentiableAt_iteratedFDeriv
    {f : E â†’ F} {n : WithTop â„•âˆ} {m : â„•} {x : E} (h : ContDiffAt ğ•œ n f x) (hmn : â†‘m < n) :
    DifferentiableAt ğ•œ (iteratedFDeriv ğ•œ m f) x := by
  rw [â† differentiableWithinAt_univ]
  convert (h.differentiableWithinAt_iteratedFDerivWithin hmn (by simp [uniqueDiffOn_univ]))
  exact iteratedFDerivWithin_univ.symm

theorem ContDiffAt.iteratedFDeriv_restrictScalars_eventuallyEq {f : E â†’ F} {n : â„•}
    (h : ContDiffAt ğ•œ' n f x) :
    (ContinuousMultilinearMap.restrictScalarsLinear ğ•œ) âˆ˜ (iteratedFDeriv ğ•œ' n f) =á¶ [ğ“ x]
      (iteratedFDeriv ğ•œ n f) := by
  induction n with
  | zero =>
    filter_upwards with a
    ext m
    simp [iteratedFDeriv_zero_apply m]
  | succ n hn =>
    have : ContDiffAt ğ•œ' n f x := h.of_le (Nat.cast_le.mpr (n.le_add_right 1))
    have tâ‚€ := hn this
    have tâ‚ := this.eventually
    simp only [ne_eq, ENat.natCast_ne_coe_top, not_false_eq_true, forall_const] at tâ‚
    filter_upwards [tâ‚€.eventually_nhds, tâ‚.eventually_nhds,
      h.eventually (by simp)] with a hâ‚a hâ‚‚a hâ‚ƒa
    rw [â† Filter.EventuallyEq] at hâ‚a
    ext m
    simp only [ContinuousMultilinearMap.restrictScalarsLinear_apply, Function.comp_apply,
      ContinuousMultilinearMap.coe_restrictScalars, iteratedFDeriv_succ_apply_left]
    rw [â† hâ‚a.fderiv_eq, fderiv_restrictScalarsLinear_comp]
    Â· simp
    Â· apply hâ‚ƒa.differentiableAt_iteratedFDeriv
      rw [Nat.cast_lt]
      simp

theorem ContDiffAt.iteratedFDeriv_restrictScalars {f : E â†’ F} {n : â„•} {z : E}
    (h : ContDiffAt ğ•œ' n f z) :
    ((ContinuousMultilinearMap.restrictScalarsLinear ğ•œ) âˆ˜ (iteratedFDeriv ğ•œ' n f)) z =
      iteratedFDeriv ğ•œ n f z :=
  h.iteratedFDeriv_restrictScalars_eventuallyEq.eq_of_nhds
