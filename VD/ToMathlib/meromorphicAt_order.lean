import Mathlib.Analysis.Meromorphic.Divisor

open Filter Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f g : ğ•œ â†’ ğ•œ} {zâ‚€ : ğ•œ}


/--
If two functions agree in a punctured neighborhood, then one if meromorphic iff
the other is meromorphic.
-/
theorem meromorphicAt_congr
  {ğ•œ : Type u_1} [NontriviallyNormedField ğ•œ]
  {E : Type u_2} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f : ğ•œ â†’ E} {g : ğ•œ â†’ E} {x : ğ•œ}
  (h : f =á¶ [ğ“[â‰ ] x] g) : MeromorphicAt f x â†” MeromorphicAt g x :=
  âŸ¨fun hf â†¦ hf.congr h, fun hg â†¦ hg.congr h.symmâŸ©

/--
When seen as meromorphic functions, analytic functions have nonnegative order.
-/
theorem AnalyticAt.meromorphicAt_order_nonneg
  {f : ğ•œ â†’ E}
  {zâ‚€ : ğ•œ}
  (hf : AnalyticAt ğ•œ f zâ‚€) :
  0 â‰¤ hf.meromorphicAt.order := by
  rw [hf.meromorphicAt_order, (by rfl : (0 : WithTop â„¤) = WithTop.map Nat.cast (0 : â„•âˆ))]
  simp

/--
The order of a sum at least the minimum of the orders of the summands.
-/
theorem MeromorphicAt.order_add {fâ‚ fâ‚‚ : ğ•œ â†’ E} {zâ‚€ : ğ•œ} (hfâ‚ : MeromorphicAt fâ‚ zâ‚€)
    (hfâ‚‚ : MeromorphicAt fâ‚‚ zâ‚€) :
    min hfâ‚.order hfâ‚‚.order â‰¤ (hfâ‚.add hfâ‚‚).order := by
  -- Handle the trivial cases where one of the orders equals âŠ¤
  by_cases hâ‚‚fâ‚: hfâ‚.order = âŠ¤
  Â· simp only [hâ‚‚fâ‚, le_top, inf_of_le_right]
    rw [(hfâ‚.add hfâ‚‚).order_congr]
    filter_upwards [hfâ‚.order_eq_top_iff.1 hâ‚‚fâ‚]
    simp
  by_cases hâ‚‚fâ‚‚: hfâ‚‚.order = âŠ¤
  Â· simp only [hâ‚‚fâ‚‚, le_top, inf_of_le_left]
    rw [(hfâ‚.add hfâ‚‚).order_congr]
    filter_upwards [hfâ‚‚.order_eq_top_iff.1 hâ‚‚fâ‚‚]
    simp
  -- General case
  lift hfâ‚.order to â„¤ using hâ‚‚fâ‚ with nâ‚ hnâ‚
  lift hfâ‚‚.order to â„¤ using hâ‚‚fâ‚‚ with nâ‚‚ hnâ‚‚
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := (hfâ‚.order_eq_int_iff nâ‚).1 hnâ‚.symm
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := (hfâ‚‚.order_eq_int_iff nâ‚‚).1 hnâ‚‚.symm
  let n := min nâ‚ nâ‚‚
  let g := (fun z â†¦ (z - zâ‚€) ^ (nâ‚ - n)) â€¢ gâ‚ +  (fun z â†¦ (z - zâ‚€) ^ (nâ‚‚ - n)) â€¢ gâ‚‚
  have hâ‚g : AnalyticAt ğ•œ g zâ‚€ := by
    apply AnalyticAt.add
    apply (AnalyticAt.zpow_nonneg (by fun_prop) (sub_nonneg.2 (Int.min_le_left nâ‚ nâ‚‚))).smul hâ‚gâ‚
    apply (AnalyticAt.zpow_nonneg (by fun_prop) (sub_nonneg.2 (Int.min_le_right nâ‚ nâ‚‚))).smul hâ‚gâ‚‚
  have : fâ‚ + fâ‚‚ =á¶ [ğ“[â‰ ] zâ‚€] ((Â· - zâ‚€) ^ n) â€¢ g := by
    filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚, (self_mem_nhdsWithin : {zâ‚€}á¶œ âˆˆ ğ“[â‰ ] zâ‚€)]
    unfold g
    simp_all [smul_add, â† smul_assoc, â† zpow_add', sub_ne_zero]
  have tâ‚€ : MeromorphicAt ((Â·  - zâ‚€) ^ n) zâ‚€ := by fun_prop
  have tâ‚ : tâ‚€.order = n := by
    rw [tâ‚€.order_eq_int_iff]
    use 1, analyticAt_const
    simp
  rw [(hfâ‚.add hfâ‚‚).order_congr this, tâ‚€.order_smul hâ‚g.meromorphicAt, tâ‚]
  exact le_add_of_nonneg_right hâ‚g.meromorphicAt_order_nonneg

/--
Helper lemma for MeromorphicAt.order_add_of_unequal_order.
-/
lemma MeromorphicAt.order_add_of_order_lt_order {fâ‚ fâ‚‚ : ğ•œ â†’ E} {zâ‚€ : ğ•œ} (hfâ‚ : MeromorphicAt fâ‚ zâ‚€)
    (hfâ‚‚ : MeromorphicAt fâ‚‚ zâ‚€) (h : hfâ‚.order < hfâ‚‚.order) :
    (hfâ‚.add hfâ‚‚).order = hfâ‚.order := by
  -- Trivial case: fâ‚‚ vanishes identically around zâ‚€
  by_cases hâ‚fâ‚‚ : hfâ‚‚.order = âŠ¤
  Â· rw [(hfâ‚.add hfâ‚‚).order_congr]
    filter_upwards [hfâ‚‚.order_eq_top_iff.1 hâ‚fâ‚‚]
    simp
  -- General case
  lift hfâ‚‚.order to â„¤ using hâ‚fâ‚‚ with nâ‚‚ hnâ‚‚
  lift hfâ‚.order to â„¤ using h.ne_top with nâ‚ hnâ‚
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := (hfâ‚.order_eq_int_iff nâ‚).1 hnâ‚.symm
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := (hfâ‚‚.order_eq_int_iff nâ‚‚).1 hnâ‚‚.symm
  rw [(hfâ‚.add hfâ‚‚).order_eq_int_iff nâ‚]
  use gâ‚ + (Â· - zâ‚€) ^ (nâ‚‚ - nâ‚) â€¢ gâ‚‚
  constructor
  Â· apply hâ‚gâ‚.add (AnalyticAt.smul _ hâ‚gâ‚‚)
    apply AnalyticAt.zpow_nonneg (by fun_prop) (sub_nonneg.2 (Int.le_of_lt (WithTop.coe_lt_coe.1 h)))
  constructor
  Â· have : (0 : ğ•œ) ^ (nâ‚‚ - nâ‚) = (0 : ğ•œ) := by
      rw [zpow_eq_zero_iff]
      rw [ne_eq, sub_eq_zero, eq_comm, â† ne_eq]
      exact ne_of_lt (WithTop.coe_lt_coe.1 h)
    simpa [this]
  Â· filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚, (self_mem_nhdsWithin : {zâ‚€}á¶œ âˆˆ ğ“[â‰ ] zâ‚€)]
    simp_all [smul_add, â† smul_assoc, â† zpow_add', sub_ne_zero]

/--
If two meromorphic functions have unequal orders, then the order of their sum is
exactly the minimum of the orders of the summands.
-/
theorem MeromorphicAt.order_add_of_unequal_order {fâ‚ fâ‚‚ : ğ•œ â†’ E} {zâ‚€ : ğ•œ} (hfâ‚ : MeromorphicAt fâ‚ zâ‚€)
    (hfâ‚‚ : MeromorphicAt fâ‚‚ zâ‚€) (h : hfâ‚.order â‰  hfâ‚‚.order) :
    (hfâ‚.add hfâ‚‚).order = min hfâ‚.order hfâ‚‚.order := by
  by_cases hâ‚ : hfâ‚.order < hfâ‚‚.order
  Â· rw [min_eq_left (le_of_lt hâ‚)]
    exact hfâ‚.order_add_of_order_lt_order hfâ‚‚ hâ‚
  Â· rw [min_eq_right (le_of_not_lt hâ‚)]
    simp_rw [AddCommMagma.add_comm fâ‚ fâ‚‚]
    exact hfâ‚‚.order_add_of_order_lt_order hfâ‚ (lt_of_le_of_ne (le_of_not_lt hâ‚) h.symm)
