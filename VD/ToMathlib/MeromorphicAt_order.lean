import Mathlib.Analysis.Meromorphic.Order
import Mathlib.Algebra.Order.WithTop.Untop0

open scoped Interval Topology
open Real Filter

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f g : ğ•œ â†’ ğ•œ} {zâ‚€ : ğ•œ}

lemma untopâ‚€_of_ne_top {a : WithTop â„¤} (ha : a â‰  âŠ¤) :
    a = a.untopâ‚€ := by
  obtain âŸ¨b, hbâŸ© := WithTop.ne_top_iff_exists.1 ha
  simp [â† hb]

theorem MeromorphicAt.order_ne_top_iff {f : ğ•œ â†’ E} {zâ‚€ : ğ•œ} (hf : MeromorphicAt f zâ‚€) :
    hf.order â‰  âŠ¤ â†” âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§
      f =á¶ [ğ“[â‰ ] zâ‚€] fun z â†¦ (z - zâ‚€) ^ (hf.order.untopâ‚€) â€¢ g z :=
  âŸ¨fun h â†¦ hf.order_eq_int_iff.1 (untopâ‚€_of_ne_top h),
    fun h â†¦ Option.ne_none_iff_exists'.2 âŸ¨hf.order.untopD 0, hf.order_eq_int_iff.2 hâŸ©âŸ©

theorem MeromorphicAt.order_pow (hf : MeromorphicAt f zâ‚€) {n : â„•} :
    (hf.pow n).order = n * hf.order := by
  induction' n with n hn
  Â· simp only [pow_zero, CharP.cast_eq_zero, zero_mul]
    rw [â† WithTop.coe_zero, MeromorphicAt.order_eq_int_iff]
    use 1, analyticAt_const
    simp
  Â· simp only [pow_add, pow_one, (hf.pow n).order_mul hf, hn, Nat.cast_add, Nat.cast_one]
    cases hf.order
    Â· aesop
    Â· norm_cast
      simp only [Nat.cast_add, Nat.cast_one]
      ring

theorem MeromorphicAt.order_zpow (hf : MeromorphicAt f zâ‚€) {n : â„¤} :
    (hf.zpow n).order = n * hf.order := by
  -- Trivial case: n = 0
  by_cases hn : n = 0
  Â· simp only [hn, zpow_zero, WithTop.coe_zero, zero_mul]
    rw [â† WithTop.coe_zero, MeromorphicAt.order_eq_int_iff]
    use 1
    simp only [Pi.one_apply, ne_eq, one_ne_zero, not_false_eq_true, zpow_zero, smul_eq_mul, mul_one,
      eventually_true, and_self, and_true]
    apply analyticAt_const
  -- Trivial case: f locally zero
  by_cases h : hf.order = âŠ¤
  Â· simp only [h, ne_eq, WithTop.coe_eq_zero, hn, not_false_eq_true, WithTop.mul_top]
    rw [MeromorphicAt.order_eq_top_iff] at *
    filter_upwards [h]
    intro y hy
    simp [hy, zero_zpow n hn]
  -- General case
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hf.order_ne_top_iff.1 h
  rw [untopâ‚€_of_ne_top h, â† WithTop.coe_mul, MeromorphicAt.order_eq_int_iff]
  use g ^ n, hâ‚g.zpow hâ‚‚g
  constructor
  Â· simp_all [zpow_eq_zero_iff hn]
  Â· filter_upwards [hâ‚ƒg]
    intro y hy
    rw [Pi.pow_apply, hy, smul_eq_mul, mul_zpow]
    congr 1
    rw [mul_comm, zpow_mul]
