import Mathlib.Analysis.Meromorphic.NormalFormAt
import Mathlib.Analysis.Analytic.IsolatedZeros

open Filter Topology

variable {ğ•œ : Type u_1} [NontriviallyNormedField ğ•œ] {E : Type u_2} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- Local identity theorem: two analytic functions agree on a punctured
neighborhood iff they agree on a neighborhood.

See `MeromorphicNFAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd` for the analogous
statement for meromorphic functions in normal form.
-/
theorem AnalyticAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd {f g : ğ•œ â†’ E} {zâ‚€ : ğ•œ}
    (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    f =á¶ [ğ“[â‰ ] zâ‚€] g â†” f =á¶ [ğ“ zâ‚€] g := by
  constructor
  Â· intro hfg
    rcases ((hf.sub hg).eventually_eq_zero_or_eventually_ne_zero) with h | h
    Â· exact Filter.eventuallyEq_iff_sub.2 h
    Â· simpa using (Filter.eventually_and.2 âŸ¨Filter.eventuallyEq_iff_sub.mp hfg, hâŸ©).exists
  Â· exact (Filter.EventuallyEq.filter_mono Â· nhdsWithin_le_nhds)

/-- Meromorphic functions that agree in a punctured neighborhood of `zâ‚€` have the same order at
`zâ‚€`. -/
theorem MeromorphicAt.order_congr {fâ‚ fâ‚‚ : ğ•œ â†’ E} {zâ‚€ : ğ•œ} (hfâ‚ : MeromorphicAt fâ‚ zâ‚€)
    (hfâ‚â‚‚ : fâ‚ =á¶ [ğ“[â‰ ] zâ‚€] fâ‚‚):
    hfâ‚.order = (hfâ‚.congr hfâ‚â‚‚).order := by
  by_cases hâ‚fâ‚ : hfâ‚.order = âŠ¤
  Â· rw [hâ‚fâ‚, eq_comm, (hfâ‚.congr hfâ‚â‚‚).order_eq_top_iff]
    rw [hfâ‚.order_eq_top_iff] at hâ‚fâ‚
    exact EventuallyEq.rw hâ‚fâ‚ (fun x => Eq (fâ‚‚ x)) hfâ‚â‚‚.symm
  Â· obtain âŸ¨n, hn : hfâ‚.order = nâŸ© := Option.ne_none_iff_exists'.mp hâ‚fâ‚
    obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (hfâ‚.order_eq_int_iff n).1 hn
    rw [hn, eq_comm, (hfâ‚.congr hfâ‚â‚‚).order_eq_int_iff]
    use g, hâ‚g, hâ‚‚g
    exact EventuallyEq.rw hâ‚ƒg (fun x => Eq (fâ‚‚ x)) hfâ‚â‚‚.symm

/-- Meromorphic functions are analytic outside of a discrete subset in the domain of
meromorphicity. -/
theorem MeromorphicOn.analyticAt_codiscreteWithin [CompleteSpace E] {f : ğ•œ â†’ E} {U : Set ğ•œ}
    (hf : MeromorphicOn f U) :
    { x | AnalyticAt ğ•œ f x } âˆˆ Filter.codiscreteWithin U := by
  rw [mem_codiscreteWithin]
  intro x hx
  rw [Filter.disjoint_principal_right, â† Filter.eventually_mem_set]
  apply (hf x hx).eventually_analyticAt.mono
  simp only [Set.mem_compl_iff, Set.mem_diff, Set.mem_setOf_eq, not_and, not_not]
  tauto

/-- Meromorphic functions have normal form outside of a discrete subset in the domain of
meromorphicity. -/
theorem MeromorphicOn.meromorphicNFAt_codiscreteWithin [CompleteSpace E] {f : ğ•œ â†’ E} {U : Set ğ•œ}
    (hf : MeromorphicOn f U) :
    { x | MeromorphicNFAt f x } âˆˆ Filter.codiscreteWithin U := by
  filter_upwards [hf.analyticAt_codiscreteWithin] with _ ha
  exact ha.meromorphicNFAt

/-- Helper lemma for `meromorphicNFAt_iff_meromorphicNFAt_of_smul_analytic`: if
`f` is meromorphic in normal form at `zâ‚€` and `g` is analytic without zero at
`zâ‚€`, then `g â€¢ f` is meromorphic in normal form at `zâ‚€`. -/
lemma MeromorphicNFAt.meromorphicNFAt_of_smul_analytic {f : ğ•œ â†’ E} {g : ğ•œ â†’ ğ•œ} {zâ‚€ : ğ•œ}
    (hf : MeromorphicNFAt f zâ‚€) (hâ‚g : AnalyticAt ğ•œ g zâ‚€) (hâ‚‚g : g zâ‚€ â‰  0) :
    MeromorphicNFAt (g â€¢ f) zâ‚€ := by
  rcases hf with hâ‚f | âŸ¨n, g_f, hâ‚g_f, hâ‚‚g_f, hâ‚ƒg_fâŸ©
  Â· left
    filter_upwards [hâ‚f]
    simp_all
  Â· right
    use n, g â€¢ g_f, hâ‚g.smul hâ‚g_f
    constructor
    Â· simp [smul_ne_zero hâ‚‚g hâ‚‚g_f]
    Â· filter_upwards [hâ‚ƒg_f]
      intro y hy
      simp [hy, smul_comm (g y) ((y - zâ‚€) ^ n) (g_f y)]

/-- If `f` is any function and `g` is analytic without zero at `zâ‚€`, then `f` is meromorphic in
normal form at `zâ‚€` iff `g â€¢ f` is meromorphic in normal form at `zâ‚€`. -/
theorem meromorphicNFAt_iff_meromorphicNFAt_of_smul_analytic
    {g : ğ•œ â†’ ğ•œ}
    {f : ğ•œ â†’ E}
    {zâ‚€ : ğ•œ}
    (hâ‚g : AnalyticAt ğ•œ g zâ‚€)
    (hâ‚‚g : g zâ‚€ â‰  0) :
    MeromorphicNFAt f zâ‚€ â†” MeromorphicNFAt (g â€¢ f) zâ‚€ := by
  constructor
  Â· exact fun hf â†¦ hf.meromorphicNFAt_of_smul_analytic hâ‚g hâ‚‚g
  Â· intro hprod
    have : f =á¶ [ğ“ zâ‚€] gâ»Â¹ â€¢ g â€¢ f := by
      filter_upwards [hâ‚g.continuousAt.preimage_mem_nhds (compl_singleton_mem_nhds_iff.mpr hâ‚‚g)]
      intro y hy
      rw [Set.preimage_compl, Set.mem_compl_iff, Set.mem_preimage,
        Set.mem_singleton_iff] at hy
      simp [hy]
    rw [meromorphicNFAt_congr this]
    exact hprod.meromorphicNFAt_of_smul_analytic (hâ‚g.inv hâ‚‚g) (inv_ne_zero hâ‚‚g)

/-- If `f` is any function and `g` is analytic without zero at `zâ‚€`, then `f` is meromorphic in
normal form at `zâ‚€` iff `g * f` is meromorphic in normal form at `zâ‚€`. -/
theorem meromorphicNFAt_iff_meromorphicNFAt_of_mul_analytic
    {f g : ğ•œ â†’ ğ•œ}
    {zâ‚€ : ğ•œ}
    (hâ‚g : AnalyticAt ğ•œ g zâ‚€)
    (hâ‚‚g : g zâ‚€ â‰  0) :
    MeromorphicNFAt f zâ‚€ â†” MeromorphicNFAt (g * f) zâ‚€ := by
  rw [â† smul_eq_mul]
  exact meromorphicNFAt_iff_meromorphicNFAt_of_smul_analytic hâ‚g hâ‚‚g

/- Private helper lemma. -/
private lemma WithTop.map_natCast_eq_zero {n : WithTop â„•}
  (hn : WithTop.map (Nat.cast : â„• â†’ â„¤) n = 0) :
  n = 0 := by
  rcases n
  Â· contradiction
  Â· simp only [WithTop.map, Option.map] at hn
    rw [Int.ofNat_eq_zero.mp (WithTop.coe_eq_zero.mp hn)]
    rfl

/-- If `f` is meromorphic in normal form at `zâ‚€`, then `f` has order zero iff it
does not vanish at `zâ‚€`. -/
theorem MeromorphicNFAt.order_eq_zero_iff {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicNFAt f x) :
    hf.meromorphicAt.order = 0 â†” f x â‰  0 := by
  constructor
  Â· intro hâ‚f
    have hâ‚‚f := hf.order_nonneg_iff_analyticAt.1 (le_of_eq hâ‚f.symm)
    apply hâ‚‚f.order_eq_zero_iff.1
    apply WithTop.map_natCast_eq_zero
    rwa [hâ‚‚f.meromorphicAt_order] at hâ‚f
  Â· intro h
    have hf' := hf
    rcases hf with hâ‚ | âŸ¨n, g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ©
    Â· have := hâ‚.eq_of_nhds
      tauto
    Â· have : n = 0 := by
        by_contra hContra
        have := Filter.EventuallyEq.eq_of_nhds hâ‚ƒg
        simp only [Pi.smul_apply', Pi.pow_apply, sub_self, zero_zpow n hContra, zero_smul] at this
        tauto
      simp only [this, zpow_zero, smul_eq_mul, one_mul] at hâ‚ƒg
      apply (hf'.meromorphicAt.order_eq_int_iff 0).2
      use g, hâ‚g, hâ‚‚g
      simp only [zpow_zero, smul_eq_mul, one_mul]
      exact hâ‚ƒg.filter_mono nhdsWithin_le_nhds

/-- Local identity theorem: two meromorphic functions in normal form agree in a
neighborhood iff they agree in a pointed neighborhood.

See `AnalytivAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd` for the analogous
statement for analytic functions.
-/
theorem MeromorphicNFAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd {f g : ğ•œ â†’ E} {zâ‚€ : ğ•œ}
    (hf : MeromorphicNFAt f zâ‚€) (hg : MeromorphicNFAt g zâ‚€) :
    f =á¶ [ğ“[â‰ ] zâ‚€] g â†” f =á¶ [ğ“ zâ‚€] g := by
  constructor
  Â· intro h
    have tâ‚€ := hf.meromorphicAt.order_congr h
    by_cases cs : hf.meromorphicAt.order = 0
    Â· rw [cs] at tâ‚€
      have Z := (hf.order_nonneg_iff_analyticAt.1 (le_of_eq cs.symm))
      have W := hg.order_nonneg_iff_analyticAt.1 (le_of_eq tâ‚€)
      exact (AnalyticAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd Z W).1 h
    Â· apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE h
      let hâ‚f := cs
      rw [hf.order_eq_zero_iff] at hâ‚f
      let hâ‚g := cs
      rw [tâ‚€, hg.order_eq_zero_iff] at hâ‚g
      simp only [not_not] at *
      rw [hâ‚f, hâ‚g]
  Â· exact (Filter.EventuallyEq.filter_mono Â· nhdsWithin_le_nhds)
